Shallow Clay Bear

High

# Delegators will lose all of their pending rewards if they delegate to a staker after undelegating and not claiming rewards

### Summary

Delegators who have accrued some rewards for delegating to a certain delegatee for some epochs will lose all of their rewards if they decide to undelegate and delegate again without claiming rewards corresponding to epochs from the first delegation.

### Root Cause

Found in [Distribute.sol#L112](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l2/staking/Distribute.sol#L112).

When a delegator delegates to a delegatee for the first time, `Distribution.notifyDelegation()` will update and set `unclaimed[delegator].unclaimedStart[delegatee]` to the `effectiveEpoch` in order to allow the protocol to know when the delegator started delegating:

```solidity
// Distribute.sol

function notifyDelegation(
        address delegatee,
        address delegator,
        uint256 effectiveEpoch,
        uint256 amount,
        uint256 totalAmount,
        uint256 remainsNumber,
        bool newDelegation
    ) public onlyL2StakingContract {
        ...

        // update unclaimed info
        if (newDelegation) {
            unclaimed[delegator].delegatees.add(delegatee);
            unclaimed[delegator].unclaimedStart[delegatee] = effectiveEpoch;
        }
    }
```

The data stored in `unclaimed[delegator].unclaimedStart[delegatee]`  is of extreme importance in order to know from which epoch a delegator is elegible to earn rewards. Such data is used as a tracker inside `Distribute.sol`'s `_claim()` :

```solidity
// Distribute.sol
 function _claim(address delegatee, address delegator, uint256 endEpochIndex) internal returns (uint256 reward) {
        require(unclaimed[delegator].delegatees.contains(delegatee), "no remaining reward");
        require(unclaimed[delegator].unclaimedStart[delegatee] <= endEpochIndex, "all reward claimed");

        for (uint256 i = unclaimed[delegator].unclaimedStart[delegatee]; i <= endEpochIndex; i++) {
            ...
            
             // if undelegated, remove delegator unclaimed info after claimed all
            if (unclaimed[delegator].undelegated[delegatee] && unclaimed[delegator].unclaimedEnd[delegatee] == i) {
                unclaimed[delegator].delegatees.remove(delegatee);
                delete unclaimed[delegator].undelegated[delegatee];
                delete unclaimed[delegator].unclaimedStart[delegatee];
                delete unclaimed[delegator].unclaimedEnd[delegatee];
                break;
            }
        }
        unclaimed[delegator].unclaimedStart[delegatee] = endEpochIndex + 1;
        emit RewardClaimed(delegator, delegatee, endEpochIndex, reward);
    }
```

As shown in the previous snippet, `unclaimed[delegator].unclaimedStart[delegatee]` serves as the initial index for the epoch from which reward claims should be triggered. Then, after claiming all corresponding epochs, `unclaimed[delegator].unclaimedStart[delegatee]` will be updated to `endEpochIndex + 1` so that it is possible to know from which epoch rewards should be claimed on the next call to `_claim()`.

However, if a delegator undelegates a certain delegatee and then delegates to him again (without having claimed the corresponding rewards accrued during the first delegation), the delegator will no longer be able to claim the rewards. This is because the call to the new delegation will trigger `Distribute.sol`'s `notifyDelegation()` again, which as shown in the initial code snippet will reset `unclaimed[delegator].unclaimedStart[delegatee]`. Because of this, the start epoch corresponding to the first delegation will be overwritten by the new effective epoch, and it won’t be possible for the claiming algorithm to allow the delegator to claim rewards corresponding to the first delegation.

### Internal pre-conditions

- A delegator has been delegating for a delegatee for some epochs, hence accumulating delegation rewards.

### External pre-conditions

None.

### Attack Path

The following situation can occur:

1. A delegator delegates some MORPH tokens to sequencer 0xaa… . The delegation is effective in epoch 1, so `unclaimed[delegator].unclaimedStart[delegatee]` will be set to epoch 1. 
2. Some epochs pass. At this point, if the delegator wanted, he could claim rewards, and they would be claimed from epoch 1 (as per the data stored in `unclaimed[delegator].unclaimedStart[delegatee]`). However, the delegator does not claim such rewards.
3. At effective epoch 6, the delegator decides to undelegate sequencer 0xaa… and allocate his delegation to another sequencer. 
4. After some additional epochs, at effective epoch 10, the delegator decides to re-delegate to the initial sequencer 0xaa…,  hence updating `unclaimed[delegator].unclaimedStart[delegatee]` to a value of 10. During this time, the delegator has never claimed the corresponding rewards for the initial delegation
5. At effective epoch 12, the delegator decides to claim all of its corresponding rewards. Theoretically, he should be able to claim rewards from the initial delegation (epoch 1 to epoch 5), and from the second delegation (epoch 10 to epoch 12). However, because `unclaimed[delegator].unclaimedStart[delegatee]` has been overriden to 10 in the second delegation, it is impossible for the delegator to claim rewards corresponding to the period from epoch 1 to epoch 5. The `_claim()` will directly begin the claiming iteration by setting the epoch to 10, and all corresponding rewards from epoch 1 to epoch 5 will be locked forever in the contract.

### Impact

The delegator loses all of its corresponding rewards for the initial delegation if he decides to re-delegate to the same delegatee. Depending on the amount of time delegated, the loss could be substantial, given that all rewards are lost.

### PoC

_No response_

### Mitigation

In order to mitigate this issue, don’t allow delegators to delegate to a certain staker if they have already undelegated him, and if they still have pending rewards to be claimed corresponding to such delegatee:

```diff

function delegateStake(address delegatee, uint256 amount) external isStaker(delegatee) nonReentrant {
        ...
	      stakerDelegations[delegatee] += amount; // Increment total MORPH delegated to a staker
        delegations[delegatee][_msgSender()] += amount; // Increment total amount delegated by the delegator to the staker
        delegators[delegatee].add(_msgSender()); // will not be added repeatedly, add delegator as a delegator to the delegatee list
	       
	      
	      if (stakerDelegations[delegatee] == amount) {
+           require(IDistribute(DISTRIBUTE_CONTRACT).queryUnclaimed(_msgSender(), delegatee) == 0, "Rewards unclaimed");
	      
            // If it is the first time the delegatee is being delegated, increment candidate number
            candidateNumber += 1;
        }

				...
				
}
```