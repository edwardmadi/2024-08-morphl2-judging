Shambolic Banana Barbel

High

# Innocent staker can lose all pending rewards by redelegating

### Summary

In `Distribute.sol`, a user who redelegates to the same staker can accidentally overwrite their `unclaimedStart` value, which will cause them to lose all their pending rewards.

### Root Cause

In `Distribute.sol`, unclaimed rewards are tracked by keeping an `unclaimedStart` and `unclaimedEnd` tracker for each delegator, in addition to tracking the proportion of a given staker's delegated amount they make up. We are able to claim for epochs starting at `unclaimedStart`, and we increment this value after we claim to ensure we can't double claim.

In [`notifyDelegation()`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l2/staking/Distribute.sol#L94-L114), we can see the logic that initially sets this `unclaimedStart` value when a new delegation is made:
```solidity
function notifyDelegation(
    address delegatee,
    address delegator,
    uint256 effectiveEpoch,
    uint256 amount,
    uint256 totalAmount,
    uint256 remainsNumber,
    bool newDelegation
) public onlyL2StakingContract {
    // update distribution info
    distributions[delegatee][effectiveEpoch].delegationAmount = totalAmount;
    distributions[delegatee][effectiveEpoch].remainsNumber = remainsNumber;
    distributions[delegatee][effectiveEpoch].delegators.add(delegator);
    distributions[delegatee][effectiveEpoch].amounts[delegator] = amount;

    // update unclaimed info
    if (newDelegation) {
        unclaimed[delegator].delegatees.add(delegatee);
        unclaimed[delegator].unclaimedStart[delegatee] = effectiveEpoch;
    }
}
```
This `newDelegation` boolean is set to true whenever a delegation is made that takes the total balance from zero to non-zero.

However, this situation is possible when a user delegates, then undelegates, and then redelegates.

In this situation, there is a requirement that the user claims their undelegated principal on `L2Staking.sol` before redelegating, but there is no requirement that the claim their rewards. In the case that they do not, the `unclaimedStart` will be incremented, and all the historical unclaimed rewards will be lost.

### Internal Preconditions

None

### External Preconditions

1. User must delegate, undelegate, and redelegate without claiming their rewards before redelegation.

### Attack Path

1. A delegator calls `delegateStake()` to a specific staker.
2. After some period of time, when rewards are accumulated, they call `undelegateStake()`.
3. They wait the required period of time and then call `claimUndelegation()` to claim their principal.
4. This allows them to later call `delegateStake()` again, with the same staker.
5. At this time, their `unclaimedStart` tracker is updated and all their historical rewards will be lost.

### Impact

An innocent user can lose all their historical rewards when the redelegate to the same staker.

### PoC

The following test can be added to `L2Staking.t.sol` to demonstrate the attack:
```solidity
function testPapa_loseRewardsOnRedelegation() public {
    // alice delegates her full balance (20 MORPH) to the first staker
    hevm.startPrank(alice);
    morphToken.approve(address(l2Staking), type(uint256).max);
    l2Staking.delegateStake(firstStaker, 20e18);
    assertEq(morphToken.balanceOf(alice), 0);
    hevm.stopPrank();

    // warp up to start time
    hevm.warp(l2Staking.rewardStartTime());
    hevm.prank(multisig);
    l2Staking.startReward();

    // epoch 1: we set rewards for epoch 0
    _update_epoch_reward(0);
    uint aliceRewards = distribute.queryUnclaimed(firstStaker, alice);
    assertEq(aliceRewards, 1e18);

    // now alice undelegates
    hevm.prank(alice);
    l2Staking.undelegateStake(firstStaker);

    // skip forward 1 week (updating epoch rewards) and claim undelegation when it unlocks
    for (uint i = 1; i <= 7; i++) {
        _update_epoch_reward(i);
    }
    hevm.warp(block.timestamp + 1 days * (l2Staking.undelegateLockEpochs() + 1));
    hevm.prank(alice);
    l2Staking.claimUndelegation();

    // alice has got her 20 MORPH back, and still has unclaimed rewards of 1e18 MORPH
    assertEq(morphToken.balanceOf(alice), 20e18);
    aliceRewards = distribute.queryUnclaimed(firstStaker, alice);
    assertEq(aliceRewards, 1e18);

    // now redelegate to same staker and her rewards fall to zero
    hevm.prank(alice);
    l2Staking.delegateStake(firstStaker, 20e18);
    aliceRewards = distribute.queryUnclaimed(firstStaker, alice);
    assertEq(aliceRewards, 0);
}
```

### Mitigation

When calling `delegateStake()`, instead of just checking that the principal stake on `L2Staking` has been claimed, we must also check that there are no unclaimed rewards on `Distribute.sol`. This can be done by checking that `distribute.queryUnclaimed(delegator, delegatee) == 0`.
