Magic Cobalt Giraffe

High

# Re-delegating Morph tokens in `L2Staking.sol` will lead to loss of accumulated rewards

## Summary
Re-delegating Morph tokens in [`L2Staking.sol`](https://github.com/sherlock-audit/2024-08-morphl2/blob/98e0ec4c5bbd0b28f3d3a9e9159d1184bc45b38d/morph/contracts/contracts/l2/staking/L2Staking.sol#L16) will lead to loss of accumulated rewards from previous reward Epochs.
## Vulnerability Detail
Users can delegate Morph tokens to sequencers using the `delegateStake()` function in `L2Staking.sol` :

```solidity
    function delegateStake(address delegatee, uint256 amount) external isStaker(delegatee) nonReentrant {
        require(amount > 0, "invalid stake amount");
        // Re-staking to the same delegatee is not allowed before claiming undelegation
        require(!_unclaimed(_msgSender(), delegatee), "undelegation unclaimed");

        stakerDelegations[delegatee] += amount;
        delegations[delegatee][_msgSender()] += amount;
        delegators[delegatee].add(_msgSender()); // will not be added repeatedly

        if (stakerDelegations[delegatee] == amount) {
            candidateNumber += 1;
        }

        uint256 beforeRanking = stakerRankings[delegatee];
        if (rewardStarted && beforeRanking > 1) {
            // update stakers and rankings
            for (uint256 i = beforeRanking - 1; i > 0; i--) {
                if (stakerDelegations[stakerAddresses[i]] > stakerDelegations[stakerAddresses[i - 1]]) {
                    address tmp = stakerAddresses[i - 1];
                    stakerAddresses[i - 1] = stakerAddresses[i];
                    stakerAddresses[i] = tmp;

                    stakerRankings[stakerAddresses[i - 1]] = i;
                    stakerRankings[stakerAddresses[i]] = i + 1;
                }
            }
        }
        uint256 effectiveEpoch = rewardStarted ? currentEpoch() + 1 : 0;

        emit Delegated(
            delegatee,
            _msgSender(),
            delegations[delegatee][_msgSender()], // new amount, not incremental
            amount,
            effectiveEpoch
        );

        // notify delegation to distribute contract
@=>      IDistribute(DISTRIBUTE_CONTRACT).notifyDelegation(
            delegatee,
            _msgSender(),
            effectiveEpoch,
            delegations[delegatee][_msgSender()],
            stakerDelegations[delegatee],
            delegators[delegatee].length(),
            delegations[delegatee][_msgSender()] == amount
        );

        // transfer morph token from delegator to this
        _transferFrom(_msgSender(), address(this), amount);

        if (
            rewardStarted && beforeRanking > latestSequencerSetSize && stakerRankings[delegatee] <= sequencerSetMaxSize
        ) {
            _updateSequencerSet();
        }
    }
```
This calls the `notifyDelegation()` function in `Distribute.sol`

```solidity
    function notifyDelegation(
        address delegatee,
        address delegator,
        uint256 effectiveEpoch,
        uint256 amount,
        uint256 totalAmount,
        uint256 remainsNumber,
        bool newDelegation
    ) public onlyL2StakingContract {
        // update distribution info
        distributions[delegatee][effectiveEpoch].delegationAmount = totalAmount;
        distributions[delegatee][effectiveEpoch].remainsNumber = remainsNumber;
        distributions[delegatee][effectiveEpoch].delegators.add(delegator);
        distributions[delegatee][effectiveEpoch].amounts[delegator] = amount;

        // update unclaimed info
        if (newDelegation) {
            unclaimed[delegator].delegatees.add(delegatee);
            unclaimed[delegator].unclaimedStart[delegatee] = effectiveEpoch;
        }
    }
```
This sets `unclaimed[delegator].unclaimedStart[delegatee] = effectiveEpoch`

Similary, if a delegator undelegates their morph tokens, `notifyUndelegation()` is called

```solidity
    function notifyUndelegation(
        address delegatee,
        address delegator,
        uint256 effectiveEpoch,
        uint256 totalAmount,
        uint256 remainsNumber
    ) public onlyL2StakingContract {
        // update distribution info
        distributions[delegatee][effectiveEpoch].delegationAmount = totalAmount;
        distributions[delegatee][effectiveEpoch].remainsNumber = remainsNumber;

        // not start reward yet, or delegate and undelegation within the same epoch, remove unclaim info
        if (effectiveEpoch == 0 || unclaimed[delegator].unclaimedStart[delegatee] == effectiveEpoch) {
            // update distribution info
            distributions[delegatee][effectiveEpoch].delegators.remove(delegator);
            delete distributions[delegatee][effectiveEpoch].amounts[delegator];

            // update unclaimed info
            unclaimed[delegator].delegatees.remove(delegatee);
            delete unclaimed[delegator].undelegated[delegatee];
            delete unclaimed[delegator].unclaimedStart[delegatee];
            delete unclaimed[delegator].unclaimedEnd[delegatee];
            return;
        }

        // update unclaimed info
        unclaimed[delegator].undelegated[delegatee] = true;
        unclaimed[delegator].unclaimedEnd[delegatee] = effectiveEpoch - 1;
    }
```

`unclaimedStart` and `unclaimedEnd` denote the range (both inclusive) of the reward epochs for which the delegator is supposed to receive delegation rewards.

The rewards are distributed using the `_claim()` function which is called through the `claim()` function in `Distribute.sol` :

```solidity
    function claim(address delegatee, address delegator, uint256 targetEpochIndex) external onlyL2StakingContract {
        require(mintedEpochCount != 0, "not minted yet");
        uint256 endEpochIndex = (targetEpochIndex == 0 || targetEpochIndex > mintedEpochCount - 1)
            ? mintedEpochCount - 1
            : targetEpochIndex;
        uint256 reward = _claim(delegatee, delegator, endEpochIndex);
        if (reward > 0) {
            _transfer(delegator, reward);
        }
    }

    function _claim(address delegatee, address delegator, uint256 endEpochIndex) internal returns (uint256 reward) {
        require(unclaimed[delegator].delegatees.contains(delegatee), "no remaining reward");
        require(unclaimed[delegator].unclaimedStart[delegatee] <= endEpochIndex, "all reward claimed");

        for (uint256 i = unclaimed[delegator].unclaimedStart[delegatee]; i <= endEpochIndex; i++) {
            // compute delegator epoch reward
            reward +=
                (distributions[delegatee][i].delegatorRewardAmount * distributions[delegatee][i].amounts[delegator]) /
                distributions[delegatee][i].delegationAmount;

            // if claimed end epoch is reached, next distribution has been updated when undelegate
            if (!unclaimed[delegator].undelegated[delegatee] || unclaimed[delegator].unclaimedEnd[delegatee] != i) {
                // if delegator has not finished the claim and distribution not contains the delegator's info in next epoch,
                // migrate delegator info to next epoch.
                if (!distributions[delegatee][i + 1].delegators.contains(delegator)) {
                    distributions[delegatee][i + 1].delegators.add(delegator);
                    distributions[delegatee][i + 1].amounts[delegator] = distributions[delegatee][i].amounts[delegator];
                }

                // if next distribution is empty, migrate distribution to next epoch
                if (distributions[delegatee][i + 1].delegationAmount == 0) {
                    distributions[delegatee][i + 1].delegationAmount = distributions[delegatee][i].delegationAmount;
                    distributions[delegatee][i + 1].remainsNumber = distributions[delegatee][i].remainsNumber;
                }
            }

            // update distribution info, delete if all claimed
            distributions[delegatee][i].remainsNumber--;
            if (
                distributions[delegatee][i].remainsNumber == 0 && // all delegators claimed
                distributions[delegatee][i].commissionAmount == 0 // delegatee claimed
            ) {
                delete distributions[delegatee][i];
            }

            // if undelegated, remove delegator unclaimed info after claimed all
            if (unclaimed[delegator].undelegated[delegatee] && unclaimed[delegator].unclaimedEnd[delegatee] == i) {
                unclaimed[delegator].delegatees.remove(delegatee);
                delete unclaimed[delegator].undelegated[delegatee];
                delete unclaimed[delegator].unclaimedStart[delegatee];
                delete unclaimed[delegator].unclaimedEnd[delegatee];
                break;
            }
        }
        unclaimed[delegator].unclaimedStart[delegatee] = endEpochIndex + 1;
        emit RewardClaimed(delegator, delegatee, endEpochIndex, reward);
    }
```

Now, consider the following scenario :

Epoch 0 : User delegates tokens to delegatee `unclaimedStart` is set to 1
Epoch 5 : User undelegates their funds, `unclaimedEnd` is set to 5

User is owed rewards from Epoch 1-5

Epoch 6 : User delegates again, `unclaimedStart` is set to `7`

This means that the rewards from epoch 1-5 are no longer claimable and are lost forever, since `_claim` will always start accumulating rewards from `unclaimedStart`

## Impact
Permanent loss of funds
## Code Snippet

## Tool used

Manual Review

## Recommendation
Revert `notifyDelegation()` if `unclaimedStart > 0` and `newDelegation == true`