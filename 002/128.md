Tangy Navy Hornet

High

# Unclaimed rewards can be lost after a redelegation

### Summary

Unclaimed rewards can be lost after a redelegation

### Root Cause

The idea behind Morph's staking system is that a user's delegation status should not affect claiming of rewards, meaning that  he should still be able to claim his rewards regardless of whether he is delegated or undelegated

However, if the user is redelegating, the user will lose his unclaimed rewards due to a bug in the system. There are many reasons why a user might want to redelegate. For example, if they want to decrease their stake in order to obtain more liquid funds, they must undelegate first and then redelegate as partial undelegations are unsupported.

Firstly, here is how reward claiming works. Primarily, if the user has undelegated, the rewards are claimed from the `unclaimedStart[delegatee]` to the `unclaimedEnd[delegatee]` epoch.

[Distribute.sol#L336-L383](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l2/staking/Distribute.sol#L336-L383)
```solidity
    function _claim(address delegatee, address delegator, uint256 endEpochIndex) internal returns (uint256 reward) {
        require(unclaimed[delegator].delegatees.contains(delegatee), "no remaining reward");
        require(unclaimed[delegator].unclaimedStart[delegatee] <= endEpochIndex, "all reward claimed");

        for (uint256 i = unclaimed[delegator].unclaimedStart[delegatee]; i <= endEpochIndex; i++) {
            // compute delegator epoch reward
            reward +=
                (distributions[delegatee][i].delegatorRewardAmount * distributions[delegatee][i].amounts[delegator]) /
                distributions[delegatee][i].delegationAmount;



            // if claimed end epoch is reached, next distribution has been updated when undelegate
            if (!unclaimed[delegator].undelegated[delegatee] || unclaimed[delegator].unclaimedEnd[delegatee] != i) {
                // if delegator has not finished the claim and distribution not contains the delegator's info in next epoch,
                // migrate delegator info to next epoch.
                if (!distributions[delegatee][i + 1].delegators.contains(delegator)) {
                    distributions[delegatee][i + 1].delegators.add(delegator);
                    distributions[delegatee][i + 1].amounts[delegator] = distributions[delegatee][i].amounts[delegator];
                }

                // if next distribution is empty, migrate distribution to next epoch
                if (distributions[delegatee][i + 1].delegationAmount == 0) {
                    distributions[delegatee][i + 1].delegationAmount = distributions[delegatee][i].delegationAmount;
                    distributions[delegatee][i + 1].remainsNumber = distributions[delegatee][i].remainsNumber;
                }
            }


            // update distribution info, delete if all claimed
            distributions[delegatee][i].remainsNumber--;
            if (
                distributions[delegatee][i].remainsNumber == 0 && // all delegators claimed
                distributions[delegatee][i].commissionAmount == 0 // delegatee claimed
            ) {
                delete distributions[delegatee][i];
            }

            // if undelegated, remove delegator unclaimed info after claimed all
            if (unclaimed[delegator].undelegated[delegatee] && unclaimed[delegator].unclaimedEnd[delegatee] == i) {
                unclaimed[delegator].delegatees.remove(delegatee);
                delete unclaimed[delegator].undelegated[delegatee];
                delete unclaimed[delegator].unclaimedStart[delegatee];
                delete unclaimed[delegator].unclaimedEnd[delegatee];
                break;
            }
        }
        unclaimed[delegator].unclaimedStart[delegatee] = endEpochIndex + 1;
        emit RewardClaimed(delegator, delegatee, endEpochIndex, reward);
    }
```

However a bug arises when the user arises when redelegating and there are unclaimed rewards. Since, `delegations[delegatee][_msgSender()] == amount`, the `newDelegation` parameter will be true and the result is that it will incorrectly overwrite `unclaimedStart[delegatee]` with the new value, leading to the loss of unclaimed rewards.

[L2Staking.sol#L281-L340](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l2/staking/L2Staking.sol#L284-L340)
```solidity
    function delegateStake(address delegatee, uint256 amount) external isStaker(delegatee) nonReentrant {
        require(amount > 0, "invalid stake amount");
        // Re-staking to the same delegatee is not allowed before claiming undelegation
        require(!_unclaimed(_msgSender(), delegatee), "undelegation unclaimed");

        stakerDelegations[delegatee] += amount;
        delegations[delegatee][_msgSender()] += amount;
        delegators[delegatee].add(_msgSender()); // will not be added repeatedly

        if (stakerDelegations[delegatee] == amount) {
            candidateNumber += 1;
        }

        uint256 beforeRanking = stakerRankings[delegatee];
        if (rewardStarted && beforeRanking > 1) {
            // update stakers and rankings
            for (uint256 i = beforeRanking - 1; i > 0; i--) {
                if (stakerDelegations[stakerAddresses[i]] > stakerDelegations[stakerAddresses[i - 1]]) {
                    address tmp = stakerAddresses[i - 1];
                    stakerAddresses[i - 1] = stakerAddresses[i];
                    stakerAddresses[i] = tmp;

                    stakerRankings[stakerAddresses[i - 1]] = i;
                    stakerRankings[stakerAddresses[i]] = i + 1;
                }
            }
        }
        uint256 effectiveEpoch = rewardStarted ? currentEpoch() + 1 : 0;

        emit Delegated(
            delegatee,
            _msgSender(),
            delegations[delegatee][_msgSender()], // new amount, not incremental
            amount,
            effectiveEpoch
        );

        // notify delegation to distribute contract
        IDistribute(DISTRIBUTE_CONTRACT).notifyDelegation(
            delegatee,
            _msgSender(),
            effectiveEpoch,
            delegations[delegatee][_msgSender()],
            stakerDelegations[delegatee],
            delegators[delegatee].length(),
            delegations[delegatee][_msgSender()] == amount
        );

        // transfer morph token from delegator to this
        _transferFrom(_msgSender(), address(this), amount);

        if (
            rewardStarted && beforeRanking > latestSequencerSetSize && stakerRankings[delegatee] <= sequencerSetMaxSize
        ) {
            _updateSequencerSet();
        }
    }
```
[Distribute.sol#L94-L114](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l2/staking/Distribute.sol#L94-L114)
```solidity
    function notifyDelegation(
        address delegatee,
        address delegator,
        uint256 effectiveEpoch,
        uint256 amount,
        uint256 totalAmount,
        uint256 remainsNumber,
        bool newDelegation
    ) public onlyL2StakingContract {
        // update distribution info
        distributions[delegatee][effectiveEpoch].delegationAmount = totalAmount;
        distributions[delegatee][effectiveEpoch].remainsNumber = remainsNumber;
        distributions[delegatee][effectiveEpoch].delegators.add(delegator);
        distributions[delegatee][effectiveEpoch].amounts[delegator] = amount;

        // update unclaimed info
        if (newDelegation) {
            unclaimed[delegator].delegatees.add(delegatee);
            unclaimed[delegator].unclaimedStart[delegatee] = effectiveEpoch;
        }
    }
```

### Internal pre-conditions

n/a

### External pre-conditions

n/a

### Attack Path

n/a

### Impact

Loss of unclaimed rewards during a redelgation.

### PoC

n/a

### Mitigation

Do not overwrite `unclaimed[delegator].unclaimedStart[delegatee]` if it is not zero.