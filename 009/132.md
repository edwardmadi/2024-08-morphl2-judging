Magic Cobalt Giraffe

High

# Unclaimed sequencer commissions will be lost forever if the sequencer withdraws on L1 or gets slashed on L1

## Summary
Unclaimed sequencer commissions will be lost forever if the sequencer withdraws on L1 or gets slashed on L1, this is because sequencers are only allowed to claim commissions if they are present in the stakers list on L2.

## Vulnerability Detail
The `claimCommission()` function in `L2Staking.sol`  is used by sequencers to claim their commissions from delegator rewards. 

```solidity 
    function claimCommission(uint256 targetEpochIndex) external onlyStaker nonReentrant {
        IDistribute(DISTRIBUTE_CONTRACT).claimCommission(_msgSender(), targetEpochIndex);
    }
```

As you can see, the function has a `onlyStaker` modifier.

```solidity
  modifier onlyStaker() {
        require(stakerRankings[_msgSender()] > 0, "only staker allowed");
        _;
    }
```
This requires the caller to be a part of the staker set in order to be able to claim the pending commissions from all L2 reward Epochs.

However, if the sequencer decides to stop their operations by calling the `withdraw()` function in `L1Staking.sol` or gets slashed, the `removeStakers()` function in `L2Staking.sol` is called through a cross domain message.

```solidity
function removeStakers(address[] calldata remove) external onlyOtherStaking {
        bool updateSequencerSet = false;
        for (uint256 i = 0; i < remove.length; i++) {
            if (stakerRankings[remove[i]] <= latestSequencerSetSize) {
                updateSequencerSet = true;
            }

            if (stakerRankings[remove[i]] > 0) {
                // update stakerRankings
                for (uint256 j = stakerRankings[remove[i]] - 1; j < stakerAddresses.length - 1; j++) {
                    stakerAddresses[j] = stakerAddresses[j + 1];
                    stakerRankings[stakerAddresses[j]] -= 1;
                }
                stakerAddresses.pop();
                delete stakerRankings[remove[i]];

                // update candidateNumber
                if (stakerDelegations[remove[i]] > 0) {
                    candidateNumber -= 1;
                }
            }

            delete stakers[remove[i]];
        }
        emit StakerRemoved(remove);

        if (updateSequencerSet) {
            _updateSequencerSet();
        }
    }
```
As you can see, this removes the sequencer from the stakers set, making `claimCommission()` uncallable. All the commission that the sequencer accrued throughout their sequencer career will be locked forever in the `Distribute.sol` contract. 

Once a staker/sequencer `withdraws()` on L1, or are slashed, they cannot add themselves back to the stakers list as the `L1Staking.sol` contract adds them to the removed list. Hence, the loss is permanet and the commissions are unretreivable.

```solidity
function withdraw() external {
        require(isActiveStaker(_msgSender()), "only active staker");
        require(withdrawals[_msgSender()] == 0, "withdrawing");

        withdrawals[_msgSender()] = block.number + withdrawalLockBlocks;
        _removeStaker(_msgSender());
        emit Withdrawn(_msgSender(), withdrawals[_msgSender()]);

 @=>     delete whitelist[_msgSender()];
 @=>     removedList[_msgSender()] = true;

        address[] memory remove = new address[](1);
        remove[0] = _msgSender();
        emit StakersRemoved(remove);

        // send message to remove staker on l2
        _msgRemoveStakers(remove);
    }
```

`register()` doesn't allow re-registering :
```solidity

    /// @notice register staker
    /// @param tmKey     tendermint pubkey
    /// @param blsKey    bls pubkey
    function register(bytes32 tmKey, bytes memory blsKey) external payable inWhitelist(_msgSender()) {
```
Other than withdrawing, slashing and removal by owner also makes the same state changes as mention above and have the same impact.

This isn't a design choice, because the stuck commissions aren't retreivable by anyone, even the admin, and sequencers should be allowed to claim their morph token commissions even after they've stopped sequencing.

An argument that can be made against this loss is that this was done in order to penalize the sequencers for submitting incorrect batch data. However, this penalization is already done on L1Staking wherein the sequencers ETH deposit, 1 ETH, is confiscated and rewarded to the appropriate party. In this case, however, the commissions are forever stuck in the `Distribute.sol` contract. Also, an honest sequencer can call `withdraw()`, not be penalized on L1, but still lose their commission on L2, if for whatever reason they decide not to claim pending commissions beforehand.

## Impact
Permanent loss of funds
## Code Snippet
https://github.com/sherlock-audit/2024-08-morphl2/blob/98e0ec4c5bbd0b28f3d3a9e9159d1184bc45b38d/morph/contracts/contracts/l2/staking/L2Staking.sol#L215
## Tool used

Manual Review

## Recommendation
Remove the `onlyStaker()` modifier.