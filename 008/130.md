Tangy Navy Hornet

High

# `startReward` can be DOSed by many stakers

### Summary

`startReward` can be DOSed by many stakers

### Root Cause

When `startReward` is called, it sorts the stakers by **insertion sort**

[L2Staking.sol#L251-L275](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l2/staking/L2Staking.sol#L251-L275)
```solidity
    /// @notice start reward
    function startReward() external onlyOwner {
        require(block.timestamp >= rewardStartTime, "can't start before reward start time");
        require(candidateNumber > 0, "none candidate");

        rewardStarted = true;

        // sort stakers by insertion sort
        for (uint256 i = 1; i < stakerAddresses.length; i++) {
            for (uint256 j = 0; j < i; j++) {
                if (stakerDelegations[stakerAddresses[i]] > stakerDelegations[stakerAddresses[j]]) {
                    address tmp = stakerAddresses[j];
                    stakerAddresses[j] = stakerAddresses[i];
                    stakerAddresses[i] = tmp;
                }
            }
        }
        // update rankings
        for (uint256 i = 0; i < stakerAddresses.length; i++) {
            stakerRankings[stakerAddresses[i]] = i + 1;
        }

        // update sequencer set
        _updateSequencerSet();
    }
```

The problem with insertion sort is that it has a worst case scenario of $O(n^2)$ and it occurs if the list is sorted in the reverse order. On the other hand, it has a best case-scenario of $O(n)$ if the list is already sorted in the correct order.

The restriction on the array length is 255, due to the `stakerSet = 255` provided in the spreadsheet in the README by the sponsor, the number of iterations in the loop can be up to 65025, making it a very gas-intensive operation that can exceed the block gas limit of 30M.

In fact, running PoC tests, the minimum number of stakers sorted in reverse order only needs to be 150 to exceed the block gas limit whereas in the base case scenario

### Internal pre-conditions

1. At least 150 stakers sorted in particular order OR
2. 235 stakers sorted in the correct order

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

`startReward` can be DOSed by many stakers, preventing the starting of distribution of Morph token rewards for epochs leading to a loss of funds.

### PoC

Two PoCs are provided

The first PoC demonstrates a reverse order sorting, where length is the number of `stakers` to set:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

import "forge-std/console.sol";

contract StakerDOS {

    /// @notice staker's total delegation amount
    mapping(address staker => uint256 totalDelegationAmount) public stakerDelegations;

    uint256 length = 150;

    /// @notice sync from l1 staking
    address[] public stakerAddresses;

    function setUp() public {
        for (uint256 i = 0; i < length; i++) {
            // The insertion sort orders the highest delegations first and the lowest delegations last.
            // Therefore, the reverse order is the lowest delegation first and the highest delegation last.
            stakerAddresses.push(address(uint160(i)));
            stakerDelegations[address(uint160(i))] = i;
        }
    }

    function test_staker_dos() public {
        uint256 gasBefore = gasleft();
        console.log("Gas before: ", gasBefore);
        for (uint256 i = 1; i < stakerAddresses.length; i++) {
            for (uint256 j = 0; j < i; j++) {
                if (stakerDelegations[stakerAddresses[i]] > stakerDelegations[stakerAddresses[j]]) {
                    address tmp = stakerAddresses[j];
                    stakerAddresses[j] = stakerAddresses[i];
                    stakerAddresses[i] = tmp;
                }
            }
        }
        uint256 gasAfter = gasleft();
        console.log("Gas after: ", gasAfter);
        console.log("Gas consumed: ", gasBefore - gasAfter);
    }
}
```
Running the test obtains the following results
| Number of stakers    | Gas consumed |
| -------- | ------- |
| 150  | 30,922,953    |
| 255  | 88,071,058    |

The second PoC demonstrates the gas consumed if the original sorting is correct resulting in the best case scenario.
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

import "forge-std/console.sol";

contract StakerDOS2 {

    /// @notice staker's total delegation amount
    mapping(address staker => uint256 totalDelegationAmount) public stakerDelegations;

    uint256 length = 235;

    /// @notice sync from l1 staking
    address[] public stakerAddresses;

    function setUp() public {
        for (uint256 i = length; i > 0; i--) {
            uint256 j = i - 1;
            // The insertion sort orders the highest delegations first and the lowest delegations last.
            stakerAddresses.push(address(uint160(j)));
            stakerDelegations[address(uint160(j))] = j;
        }
    }

    function test_staker_dos() public {
        uint256 gasBefore = gasleft();
        console.log("Gas before: ", gasBefore);
        for (uint256 i = 1; i < stakerAddresses.length; i++) {
            for (uint256 j = 0; j < i; j++) {
                if (stakerDelegations[stakerAddresses[i]] > stakerDelegations[stakerAddresses[j]]) {
                    address tmp = stakerAddresses[j];
                    stakerAddresses[j] = stakerAddresses[i];
                    stakerAddresses[i] = tmp;
                }
            }
        }
        uint256 gasAfter = gasleft();
        console.log("Gas after: ", gasAfter);
        console.log("Gas consumed: ", gasBefore - gasAfter);
    }
}
```
Running the test obtains the following results
| Number of stakers    | Gas consumed |
| -------- | ------- |
| 235  | 30,295,923    |
| 255  | 35,592,163    |

So what does this tells us? We can conclude that
- A minimum of 150 stakers are required to DoS, if they are sorted in the reverse order.
- For any particular ordering, if there are at least 235 stakers, DoS is always guaranteed to occur.

### Mitigation

Refactor the sorting algorithm so that it doesn't consume so much gas.