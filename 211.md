Odd Plum Crab

Medium

# Malicious Sequencers can force a hard fork by forcing the owner to not be able to revert batches when a challenge would be lost.

### Summary

When a challenge is lost, [the `revertReqIndex` is set to the `batchIndex`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/rollup/Rollup.sol#L701-L702) to flag that is required to revert unfinalized batches starting from that index until the `lastFinalizedBatchIndex`.

```solidity
modifier nonReqRevert() {
    require(revertReqIndex == 0, "need revert");
    _;
}

function revertBatch(bytes calldata _batchHeader, uint256 _count) external onlyOwner {
    ...

    while (_count > 0) {
        ...

        //@audit => Only the revertBatch() set the `revertReqIndex` back to 0 after a challlenge has been lost!
        if (revertReqIndex > 0 && _batchIndex == revertReqIndex) {
            revertReqIndex = 0;
        }

        ...
    }
}

function _challengerWin(uint256 batchIndex, uint256 sequencersBitmap, string memory _type) internal {
    //@audit => The batchIndex of the batch that lost the challenge must be reverted!
    revertReqIndex = batchIndex;
    ..
}
```


When the owner reverts a batch, the reverting must start from the ending, or in other words, the owner needs to revert from the last submitted batch until the batchIndex of the batch that lost the challenge, [otherwise, the revertBatch reverts with th error: "reverting must start from the ending".](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/rollup/Rollup.sol#L334-L335)
So, if the reverting must start from the ending, if there is a huge amount of unfinalized batches, the [`revertBatch() function`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/rollup/Rollup.sol#L326-L364) can be forced to revert due to an OOG for the huge iterations that would need to do to reset the value of each `committedBatches[index]` to the default value, and the rest of the logic executed in the while loop.

[`Rollup.revertBatch() function`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/rollup/Rollup.sol#L326-L335)
```solidity
function revertBatch(bytes calldata _batchHeader, uint256 _count) external onlyOwner {
    ...

    //@audit => Must revert from the specified batchIndex till the last commited batch!
    // make sure no gap is left when reverting from the ending to the beginning.
    require(committedBatches[_batchIndex + _count] == bytes32(0), "reverting must start from the ending");
    
    //@audit-ok => Can't revert finalized batches!
    // check finalization
    require(_batchIndex > lastFinalizedBatchIndex, "can only revert unFinalized batch");

    lastCommittedBatchIndex = _batchIndex - 1;

    //@audit-issue => Must iterate over all the unfinalized batches till the last one till the index of the batch that lost the challenge.
    while (_count > 0) {
        emit RevertBatch(_batchIndex, _batchHash);

        committedBatches[_batchIndex] = bytes32(0);
        // if challenge exist and not finished yet, return challenge deposit to challenger
        if (!challenges[_batchIndex].finished) {
            batchChallengeReward[challenges[_batchIndex].challenger] += challenges[_batchIndex].challengeDeposit;
            inChallenge = false;
        }
        delete challenges[_batchIndex];

        if (revertReqIndex > 0 && _batchIndex == revertReqIndex) {
            revertReqIndex = 0;
        }

        unchecked {
            _batchIndex += 1;
            _count -= 1;
        }
        _batchHash = committedBatches[_batchIndex];
        if (_batchHash == bytes32(0)) {
            break;
        }
    }
}
```

The problem of DoSing the [`revertBatch() function`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/rollup/Rollup.sol#L326-L364) is that the functions `commitBatch()`, `challengeState()`, `proveState()` && `finalizeBatch()` are unnusable because the [`nonReqRevert() modifier`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/rollup/Rollup.sol#L111-L115) is waiting for the owner to revert the unfinalized batches starting from the batch that lost the challenge. **While the `revertBatch()` is not executed successfully, the `nonReqRevert()` modifier will be reverting the tx because of the lost challenged.**

The attack path is very straight forward, the malicious sequencer(s) commits hundred/thousands of batches while the challenge is going, so that the difference between the index of the batch in challenge and the `lastCommittedBatchIndex` would be so immense that the `revertBatch()` will reverts because a gas error.

```solidity
function commitBatch(
    BatchDataInput calldata batchDataInput,
    BatchSignatureInput calldata batchSignatureInput
    //@audit => Only an active staker
    //@audit => Only if it is not required to revert batches (if a challenger wins a challenge is required to revert batches!)
) external payable override OnlyActiveStaker nonReqRevert whenNotPaused {

    ...
    //@audit => Without resetting the below variables to their default value, it won't be possible to commit more batches.
    //@audit-issue => The only way to reset the values is in the revertBatch()!
    require(committedBatches[_batchIndex] == _parentBatchHash, "incorrect parent batch hash");
    require(committedBatches[_batchIndex + 1] == bytes32(0), "batch already committed");
    //@audit => batchIndex must be sequencial and respect the order!
    require(_batchIndex == lastCommittedBatchIndex, "incorrect batch index");

    ...
}
```

### Root Cause

Unlimited amount of unfinalized batches can be submitted till the point the [`revertBatch() function`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/rollup/Rollup.sol#L326-L364) would revert because of the huge amount of iterations it would take to reset the `commitedBatches[index]` to the default value && set the `revertReqIndex` to 0

### Internal pre-conditions

none

### External pre-conditions

none

### Attack Path

1. Sequencer submits a malicious batch that gets challenge.
2. Sequencer spams submissions of batches.
3. Challenge is lost and the contract is forced to wait for the owner to revert the batch that lost the challenge and all the batches that were submitted afterwards.
4. Owner attempts to revert the batches, but the execution reverts because an Out Of Gas error due to the huge amount of unfinalized batches.
5. Functions `commitBatch()`, `challengeState()`, `proveState()` && `finalizeBatch()` are unnusable because the `nonReqRevert() modifier` is waiting for the owner to revert the unfinalized batches starting from the batch that lost the challenge.
6. A Hardfork would be required to introduce a new function that would allow to revert the unfinalized batches without needing to iterate over all the unfinalized batches.

### Impact

Malicious sequencers can force a hard fork to fix a DoS state on the Rollup contract.


### PoC

All the details are in the summary section.


### Mitigation

The recommendation would be to set an upper limit on the amount of `unfinalized/unconfirmed` batches that can be submitted.
- For example, if the `lastFinalizedBatchIndex` is 100, set a limit of a couple hundred unfinalized batches to prevent malicious sequencers from submitting enough batches to cause the `revertBatch()` to revert because an OOG error.

[`Rollup.commitBatch()`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/rollup/Rollup.sol#L201-L322)
```solidity
function commitBatch(
    ...
) external payable override OnlyActiveStaker nonReqRevert whenNotPaused {
  //@audit => This check would prevent from spamming batches to force an OOG error in case it is required to revert batches because of a lost challenge!
  require(lastCommittedBatchIndex - lastFinalizedBatchIndex <= MAX_UNFINALIZED_BATCHES, 'unfinalized batches limit has been reached');
  ...
}
```