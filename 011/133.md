Magic Cobalt Giraffe

High

# `L1ReverseCustomGateway.sol::onDropMessage()` will revert, leading to locked ERC20 tokens

## Summary
`L1ReverseCustomGateway.sol` incorrectly inherits `L1ERC20Gateway.sol`'s `onDropMessage()` function, and this will lead to locked funds if the bridge request fails or gets skipped.
## Vulnerability Detail
`L1ReverseCustomGateway.sol` is used to bridge ERC20 tokens to L2, by burning the tokens on the L1 side.

```solidity
function _deposit(
        address _token,
        address _to,
        uint256 _amount,
        bytes memory _data,
        uint256 _gasLimit
    ) internal virtual override nonReentrant {
        address _l2Token = tokenMapping[_token];
        require(_l2Token != address(0), "no corresponding l2 token");

        // 1. Transfer token into this contract.
        address _from = _msgSender();
        if (router == _from) {
            (_from, _data) = abi.decode(_data, (address, bytes));
        }

        // 2. Burn token.
        IMorphERC20Upgradeable(_token).burn(_from, _amount);

        // 2. Generate message passed to L2ReverseCustomGateway.
        bytes memory _message = abi.encodeCall(
            IL2ERC20Gateway.finalizeDepositERC20,
            (_token, _l2Token, _from, _to, _amount, _data)
        );

        uint256 nonce = IL1CrossDomainMessenger(messenger).messageNonce();
        // 3. Send message to L1CrossDomainMessenger.
        IL1CrossDomainMessenger(messenger).sendMessage{value: msg.value}(counterpart, 0, _message, _gasLimit, _from);

        emit DepositERC20(_token, _l2Token, _from, _to, _amount, _data, nonce);
    }
```
Now, if this message fails to get executed on L2, the user can call `dropMessage()` in `L1CrossDomainMessenger.sol` to get their tokens back. 

```solidity
    function dropMessage(
        address _from,
        address _to,
        uint256 _value,
        uint256 _messageNonce,
        bytes memory _message
    ) external override whenNotPaused notInExecution {
        // The criteria for dropping a message:
        // 1. The message is a L1 message.
        // 2. The message has not been dropped before.
        // 3. the message and all of its replacement are finalized in L1.
        // 4. the message and all of its replacement are skipped.
        //
        // Possible denial of service attack:
        // + replayMessage is called every time someone want to drop the message.
        // + replayMessage is called so many times for a skipped message, thus results a long list.
        //
        // We limit the number of `replayMessage` calls of each message, which may solve the above problem.

        address _messageQueue = messageQueue;

        // check message exists
        bytes memory _xDomainCalldata = _encodeXDomainCalldata(_from, _to, _value, _messageNonce, _message);
        bytes32 _xDomainCalldataHash = keccak256(_xDomainCalldata);
        require(messageSendTimestamp[_xDomainCalldataHash] > 0, "Provided message has not been enqueued");

        // check message not dropped
        require(!isL1MessageDropped[_xDomainCalldataHash], "Message already dropped");

        // check message is finalized
        uint256 _lastIndex = replayStates[_xDomainCalldataHash].lastIndex;
        if (_lastIndex == 0) _lastIndex = _messageNonce;

        // check message is skipped and drop it.
        // @note If the list is very long, the message may never be dropped.
        while (true) {
            IL1MessageQueue(_messageQueue).dropCrossDomainMessage(_lastIndex);
            _lastIndex = prevReplayIndex[_lastIndex];
            if (_lastIndex == 0) break;
            unchecked {
                _lastIndex = _lastIndex - 1;
            }
        }

        isL1MessageDropped[_xDomainCalldataHash] = true;

        // set execution context
        xDomainMessageSender = Constants.DROP_XDOMAIN_MESSAGE_SENDER;
        IMessageDropCallback(_from).onDropMessage{value: _value}(_message);
        // clear execution context
        xDomainMessageSender = Constants.DEFAULT_XDOMAIN_MESSAGE_SENDER;
    }
```
This calls `onDropMessage()` in `L1ReverseCustomGateway.sol` :
```solidity
    function onDropMessage(bytes calldata _message) external payable virtual onlyInDropContext nonReentrant {
        // _message should start with 0x8431f5c1  =>  finalizeDepositERC20(address,address,address,address,uint256,bytes)
        require(bytes4(_message[0:4]) == IL2ERC20Gateway.finalizeDepositERC20.selector, "invalid selector");

        // decode (token, receiver, amount)
        (address _token, , address _receiver, , uint256 _amount, ) = abi.decode(
            _message[4:],
            (address, address, address, address, uint256, bytes)
        );

        // do dome check for each custom gateway
        _beforeDropMessage(_token, _receiver, _amount);

        IERC20Upgradeable(_token).safeTransfer(_receiver, _amount);

        emit RefundERC20(_token, _receiver, _amount);
    }
```
However, the `onDropMessage()` logic is incorrect as the tokens weren't transferred from the user but instead burnt, but clearly this function is incorrectly calling `safeTransfer()` when it should be minting the tokens back to the user. This will cause a revert since the contract doesn't hold the corresponding amount of tokens and the user's tokens will get lost forever.
## Impact
Permanent loss of funds

## Code Snippet
[L1ReverseCustomGateway.sol](https://github.com/sherlock-audit/2024-08-morphl2/blob/98e0ec4c5bbd0b28f3d3a9e9159d1184bc45b38d/morph/contracts/contracts/l1/gateways/L1ReverseCustomGateway.sol#L13)
## Tool used

Manual Review

## Recommendation
Add a new `onDropMessage()` function to the `L1ReverseCustomGateway.sol` contract that mints the tokens back to the user.
