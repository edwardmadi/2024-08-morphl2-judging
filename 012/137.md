Magic Cobalt Giraffe

High

# L1 block re-org could cause an honest challenger to lose their `challengeDeposit`

I would like to preface this finding by pointing out that issues related to L1 block re-orgs are valid in this particular contest according to the Readme : 
```md
Additional audit information.

Diff after ToB audit:
https://github.com/morph-l2/morph/compare/tob_auditâ€¦main
The Morph L2 will not re-org by design, so issues of L2 re-org are invalid. But if there is any issue about Ethereum L1 re-org leading to financial loss, that issue is valid.
```
## Summary
L1 block re-org could cause an honest challenger to lose their `challengeDeposit`, this is because `Rollup.sol::challengeState()` identifies committed batches through the `batchIndex` parameter and nothing else.

## Vulnerability Detail
Batches are committed in a sequential manner in `Rollup.sol`, i.e, the parent of a batch at `batchIndex` exists at `batchIndex-1` :

```solidity
     (uint256 _batchPtr, bytes32 _parentBatchHash) = _loadBatchHeader(batchDataInput.parentBatchHeader);
        uint256 _batchIndex = BatchHeaderCodecV0.getBatchIndex(_batchPtr);
        require(committedBatches[_batchIndex] == _parentBatchHash, "incorrect parent batch hash");
        require(committedBatches[_batchIndex + 1] == bytes32(0), "batch already committed");
        require(_batchIndex == lastCommittedBatchIndex, "incorrect batch index");
```

Malicious batches can be challenged by a whitelisted challenger by using [`challengeState()`](https://github.com/sherlock-audit/2024-08-morphl2/blob/98e0ec4c5bbd0b28f3d3a9e9159d1184bc45b38d/morph/contracts/contracts/l1/rollup/Rollup.sol#L367) :
```solidity
    function challengeState(uint64 batchIndex) external payable onlyChallenger nonReqRevert whenNotPaused {
        require(!inChallenge, "already in challenge");
        require(lastFinalizedBatchIndex < batchIndex, "batch already finalized");
        require(committedBatches[batchIndex] != 0, "batch not exist");
        require(challenges[batchIndex].challenger == address(0), "batch already challenged");
        // check challenge window
        require(batchInsideChallengeWindow(batchIndex), "cannot challenge batch outside the challenge window");
        // check challenge amount
        require(msg.value >= IL1Staking(l1StakingContract).challengeDeposit(), "insufficient value");

        batchChallenged = batchIndex;
        challenges[batchIndex] = BatchChallenge(batchIndex, _msgSender(), msg.value, block.timestamp, false, false);
        emit ChallengeState(batchIndex, _msgSender(), msg.value);

        for (uint256 i = lastFinalizedBatchIndex + 1; i <= lastCommittedBatchIndex; i++) {
            if (i != batchIndex) {
                batchDataStore[i].finalizeTimestamp += proofWindow;
            }
        }

        inChallenge = true;
    }
```
As you can see, unlike functions that use the entire batch header to identify a batch, `challengeState()` only uses the `batchIndex`, which makes it vulnerable to L1 re-rgs.


Consider the following situation :

1. An honest staker calls `commitBatch()`
2. An MEV bot/attacker observes the transaction in the mempool and quickly forges a malicious `batchHeader` and front-runs the honest staker with a `commitBatch()` call. The attacker increases the gas fee, and their transaction gets included 1 block before the honest staker. 

Note that this type of front-running attack is possible : [source](https://www.immunebytes.com/blog/front-running-attacks-in-blockchain-the-complete-guide/#:~:text=In%20most%20blockchain,front%2Drunning%20attack.)

The incentive for an attacker to do so is to change certain aspects of the L2 state that provide benefit to them.

Now, consider the following unfinalized blocks :

- Block 1 : Attacker's transaction gets processed, and the batch is committed at `batchIndex` 
- Block 2 : Honest staker's transaction reverts, as `batchIndex` is already occupied by the malicious batch.
- Block 3 : An honest challenger observes the malicious activity, and calls `challengeState()` at the `batchIndex`

Normally, the blocks would execute in the order 1->2->3, and the challenger would successfully slash the malicious staker and receive their `challengeDeposit` back along with the malicious staker's `stakingValue` as a reward for the challenge.

L1 block re-org occurs and now the execution is in the order 2->1->3

- Block 2 : Honest staker's transaction succeeds
- Block 1 : Attackers transaction fails
- Block 3 : Honest challenger challenges `batchIndex`, but this `batchIndex` isn't malicious anymore

After this, a prover can call `proveState()` and prove the committed batch's validity, the honest challenger is slashed for `challengeDeposit` and the prover gets it as a reward.

## Impact
Permanent loss of funds for an honest challenger
## Code Snippet

## Tool used

Manual Review

## Recommendation

Alter `challengeState()` so that it takes the entire `batchHeader` as input, instead of just `batchIndex`, as this will validate the committed batch with its hash, which will avoid a situation in which a challenger unintentionally challenges a valid batch. This is already done in `proveState()` and `commitBatch()` :

```solidity
function proveState(
        bytes calldata _batchHeader,
        bytes calldata _aggrProof,
        bytes calldata _kzgDataProof
    ) external nonReqRevert whenNotPaused {
        // get batch data from batch header
        (uint256 memPtr, bytes32 _batchHash) = _loadBatchHeader(_batchHeader);
        // check batch hash
        uint256 _batchIndex = BatchHeaderCodecV0.getBatchIndex(memPtr);
        require(committedBatches[_batchIndex] == _batchHash, "incorrect batch hash");
```