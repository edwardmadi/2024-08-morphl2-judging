Quaint Mahogany Bat

High

# Staker can block all other stakers from withdrawing their stake, profiting from it and in the worst case finalize a faulty batch

### Summary

A staker working with a challenger can steal funds from the staking contract, preventing all other stakers from withdrawing their stake. If the staker set is very small, this can even be leveraged to finalize a faulty batch, allowing the staker to drain the `L1CrossDomainMessenger`.

This is done by committing an amount of batches, high enough that `revertBatch` cannot revert all of them within one transaction due to the block gas limit.


### Root Cause

The rootcause is that the function [`L1Staking::getStakersFromBitmap`](https://github.com/sherlock-audit/2024-08-morphl2/blob/98e0ec4c5bbd0b28f3d3a9e9159d1184bc45b38d/morph/contracts/contracts/l1/staking/L1Staking.sol#L387-L391) does no check for whether `stakerSet[i - 1]` is equal to `address(0)`.

```solidity
function getStakersFromBitmap(uint256 bitmap) public view returns (address[] memory stakerAddrs) {
    
    // [...]

    stakerAddrs = new address[](stakersLength);
    uint256 index = 0;
    for (uint8 i = 1; i < 255; i++) {
        if ((bitmap & (1 << i)) > 0) {
            stakerAddrs[index] = stakerSet[i - 1]; // [1] <-----
            index = index + 1;
            if (index >= stakersLength) {
                break;
            }
        }
    }
}
```

Here at `[1]`, `stakerSet[i - 1]` is taken as the `stakerAddrs[index]`. Now if this is `address(0)`, this causes `slash` to call `_removeStaker` with `address(0)`.

```solidity
function slash(uint256 sequencersBitmap) external onlyRollupContract nonReentrant returns (uint256) {
    address[] memory sequencers = getStakersFromBitmap(sequencersBitmap);

    uint256 valueSum;
    for (uint256 i = 0; i < sequencers.length; i++) {
        if (withdrawals[sequencers[i]] > 0) {
            // [...]
        } else if (!isStakerInDeleteList(sequencers[i])) {
            // If it is the first time to be slashed
            valueSum += stakingValue;
            _removeStaker(sequencers[i]); // <-----
            // [...]
        }
    }
    // [...]
}
```

`_removeStaker` will then push `address(0)` to the `deleteList`.

```solidity
function _removeStaker(address addr) internal {
    require(deleteableHeight[addr] == 0, "already in deleteList");
    deleteList.push(addr); // <-----
    deleteableHeight[addr] = block.number + withdrawalLockBlocks;
}
```

This will cause two things:

1) `_cleanStakerStore` in `claimWithdrawal` will always revert since it tries to delete `stakerSet[stakerIndexes[deleteList[i]] - 1]`. Since `address(0)` has no index in `stakerIndexes`, it will return `0`, reverting with a `arithmetic underflow or overflow` error as it will calculate `0 - 1`.
2) `slash` will call `_transfer`, sending `reward` ether to the rollup even though no new staker was slashed, reducing the staking contract's balance, making it impossible for one staker to withdraw, even if the first impact were not to happen.


### Internal pre-conditions

Described with context in `Attack Path` as there are multiple conditions which can be triggered in certain circumstances


### External pre-conditions

Described with context in `Attack Path`


### Attack Path

This bug can be maliciously triggered by a staker, sacrificing their stake of `1 ETH`. It is easier exploitable if they have controll over or work together with one challenger which would need to act non-maliciously but in a certain way, not loosing any funds.

It can be exploited in the following way:

* a malicious staker (`alice`) submits many faulty batches -> so many that one call to `revertBatch` will not succeed due to the gas limit
* challenger challenges the last faulty batch instead of the first one, which increases the other batches' `finalizeTimestamp` by `proofWindow` and puts the challenged batch into a proof period of `proofWindow`
* `alice` cannot (and does not want to) prove their batch, so the challenge succeeds after `proofWindow`, requiring the admin to revert the faulty batches
* this sets `alice`'s `deleteableHeight` to `block.number + withdrawalLockBlocks`

* since there are so many faulty batches, the admin cannot revert them all in one call as it would revert with `OOG`
* assume the following batch layout (all are faulty):
  * `[x0][x1][x2][x3][x4]`
* to simplify, we assume the admin can only revert 3 batches at once due to the gas limit
* they therefore need to revert `[x2 : x4]`
* since `x4` was challenged, this transaction causes the rollup to resume operations (code snippet below) but there are still faulty batches (no problem normally since they can be challenged due to the `proofWindow` extension)

* now a challenger challenges one of the remaining faulty batches which have been submitted by `alice` which will extend the `finalizeTimestamp` of all batches by `proofWindow` again
* now there are multiple possible cases where the issue arises:
  1) `proofWindow > withdrawalLockBlocks` (possible with value ranges defined in `README`)
  2) `proofWindow < withdrawalLockBlocks` and admin takes `withdrawalLockPeriod - proofWindow` time to call `revertBatch`
* this means that at this point, alice's `deleteableHeight` has expired and all her mapping entries like in `stakerSet` can be deleted
* now there are two more possible triggers if one of the above is fulfilled:
  1) admin calls `cleanStakerStore` to prune `alice` from all the mappings
  2) some other staker calls `claimWithdrawal`, claiming their stake
* either way, `alice` is now not existing anymore in any mappings

* at this point the bug gets triggered once the `proofWindow` expires as `alice` is not existing in `stakerSet` anymore, causing `getStakersFromBitmap` to return `address(0)`
* we triggered our bug, preventing any stakers from withdrawing their stake


`Rollup::revertBatch`:
```solidity
// [...]
if (revertReqIndex > 0 && _batchIndex == revertReqIndex) {
    revertReqIndex = 0;
}
// [...]
```

### Impact

There are three different impacts:

1) Stakers will be unable to withdraw their stake, causing their funds to be stuck in the staking contract
2) If one challenger makes both calls to `slash` they will receive rewards twice. If now a staker and a challenger work together, the staker will lose `1 ETH` but with the current `rewardPercentage` of `80`, the challenger will receive `1.6 ETH`, allowing the two to steal `0.6 ETH` from the staking contract.
3) If there are only 2-3 stakers (including the malicious one) at the time of exploitation, this allows the malicious staker to finalize a faulty batch with the following restrictions:
   1) if there are only 2 stakers, the admin needs to call `cleanStakerStore` in order to remove `alice` from the mapping (we assume the second staker is not withdrawing, if he is, this is directly exploitable). The exploit will cause the balance of the staking contract to be too small, causing any calls to `slash` to fail as the `_transfer` call will revert
   2) if there are 3 stakers, this will be exploitable if one of the other stakers is the one triggering the call to `claimWithdrawal`. This is because they will withdraw their stake, remove `alice` from the mappings, triggering the bug and as in `1)` causes all calls to `slash` to revert


### PoC

To run the PoC please add the following test to `L1Staking.t.sol` and execute `forge test --match-test test_block_claimWithdrawal -vvvv`.

```solidity
function test_block_claimWithdrawal() external {
    // add to whitelist
    address[] memory add = new address[](2);
    add[0] = alice;
    add[1] = bob;
    hevm.prank(multisig);
    l1Staking.updateWhitelist(add, new address[](0));
    assertTrue(l1Staking.whitelist(alice));
    assertTrue(l1Staking.whitelist(bob));

    // alice register
    Types.StakerInfo memory aliceInfo = ffi.generateStakerInfo(alice);
    hevm.deal(alice, 5 * STAKING_VALUE);
    hevm.prank(alice);
    l1Staking.register{value: STAKING_VALUE}(aliceInfo.tmKey, aliceInfo.blsKey);

    // bob register
    Types.StakerInfo memory bobInfo = ffi.generateStakerInfo(bob);
    hevm.deal(bob, 5 * STAKING_VALUE);
    hevm.startPrank(bob);
    l1Staking.register{value: STAKING_VALUE}(bobInfo.tmKey, bobInfo.blsKey);
    hevm.stopPrank();

    // bob sequencer to slash
    uint256 bitmap = l1Staking.getStakerBitmap(alice);
    assertEq(bitmap, 4);
    address[] memory stakers = l1Staking.getStakersFromBitmap(bitmap);
    assertEq(stakers.length, 1);
    assertEq(stakers[0], alice);
    hevm.prank(l1Staking.rollupContract());
    l1Staking.slash(bitmap);

    // simulate expiring of withdrawalLockBlocks
    hevm.roll(block.number + l1Staking.withdrawalLockBlocks());

    // simulate someone calling _cleanStakerStore
    hevm.prank(l1Staking.owner());
    l1Staking.cleanStakerStore();

    hevm.prank(l1Staking.rollupContract());
    l1Staking.slash(bitmap);

    hevm.prank(bob);
    l1Staking.withdraw();

    hevm.roll(block.number + l1Staking.withdrawalLockBlocks() + 1);

    // this will revert, showing that users cannot withdraw their stake anymore
    hevm.prank(bob);
    l1Staking.claimWithdrawal(bob);
}
```

This will show that after executing the exploit, calls to `claimWithdrawal` will fail and `bob` cannot get his stake back.

In order to show that it is feasible to commit an amount of batches so that `revertBatch` cannot revert all of them, please execute the already existing test `test_revertBatch_succeeds` with `forge test --match-test test_revertBatch_succeeds -vvvv`. This will show that the call to `revertBatch` consumes about `34000 gas` to revert 2 batches. The block gas limit is `30_000_000`. This leads us to the following calculation:
`maxRevertableBatches = 30_000_000 / (34_000 / 2) ~= 1764 batches`. This means the malicious staker needs to just submit `1765 batches` to execute this attack which is totally feasible.

Note that the amount of batches will even naturally increase as honest stakers will also add batches, reducing the amount of batches the malicious staker needs to submit.


### Mitigation

Consider adding a check to the `else if (!isStakerInDeleteList(sequencers[i]))` in `L1Staking::slash` for whether `sequencers[i] == 0`. Also consider adding a check for whether `removedList[sequencers[i]] == true`. Now this second check should not be needed if the first one is implemented but it does not hurt and could potentially prevent other exploits from being executed.
