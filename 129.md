Tangy Navy Hornet

High

# Governance can be bricked by a single voter.

### Summary

Governance can be bricked by a single voter.

### Root Cause

Governance can be bricked by a single voter by spamming unbounded proposals making the contract become entirely useless.

Particularly when executing the proposal, there is a `for` loop which will delete all proposal data from the previously passed proposal to the current proposal.

[Gov.sol#L226-L265](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l2/staking/Gov.sol#L226-L265)
```solidity
    /// @notice execute a passed proposal
    function _executeProposal(uint256 proposalID) internal {
        if (batchBlockInterval != proposalData[proposalID].batchBlockInterval) {
            uint256 _oldValue = batchBlockInterval;
            batchBlockInterval = proposalData[proposalID].batchBlockInterval;
            emit BatchBlockIntervalUpdated(_oldValue, proposalData[proposalID].batchBlockInterval);
        }
        if (batchMaxBytes != proposalData[proposalID].batchMaxBytes) {
            uint256 _oldValue = batchMaxBytes;
            batchMaxBytes = proposalData[proposalID].batchMaxBytes;
            emit BatchMaxBytesUpdated(_oldValue, proposalData[proposalID].batchMaxBytes);
        }
        if (batchTimeout != proposalData[proposalID].batchTimeout) {
            uint256 _oldValue = batchTimeout;
            batchTimeout = proposalData[proposalID].batchTimeout;
            emit BatchTimeoutUpdated(_oldValue, proposalData[proposalID].batchTimeout);
        }
        if (maxChunks != proposalData[proposalID].maxChunks) {
            uint256 _oldValue = maxChunks;
            maxChunks = proposalData[proposalID].maxChunks;
            emit MaxChunksUpdated(_oldValue, proposalData[proposalID].maxChunks);
        }
        if (rollupEpoch != proposalData[proposalID].rollupEpoch) {
            uint256 _oldValue = rollupEpoch;
            rollupEpoch = proposalData[proposalID].rollupEpoch;
            rollupEpochUpdateTime = block.timestamp;
            emit RollupEpochUpdated(_oldValue, proposalData[proposalID].rollupEpoch);
        }
        proposalInfos[proposalID].executed = true;

        // when a proposal is passed, the previous proposals will be invalidated and deleted
=>      for (uint256 i = undeletedProposalStart; i < proposalID; i++) {
            delete proposalData[i];
            delete proposalInfos[i];
            delete votes[i];
        }
        undeletedProposalStart = proposalID;

        emit ProposalExecuted(proposalID, batchBlockInterval, batchMaxBytes, batchTimeout, maxChunks, rollupEpoch);
    }
```

Thus a single voter can continuously create proposals, which can cause the deletion of proposals to exceed the block gas limit. 
The `ProposalData` struct costs 5 storage slots, the `ProposalInfo` struct costs 2 storage slots, with the caveat that 1 of them will be set to zero initially, and finally deleting the `votes` will cost N storage slots, where N is the number of voters. At minimum there will be at least 6 zero to non-zero SSTOREs per loop translating to 30k gas per proposal. A malicious voter will need to create at maximum ~1000 proposals to exceed the 30M block gas limit and permanently DoS this function and make the contract useless.

### Internal pre-conditions

Must be eligible to vote (a sequencer)

### External pre-conditions

n/a

### Attack Path

1. Voter creates 1000 proposals
2. The next proposal that passes cannot be executed, bricking the entire contract.

### Impact

Governance can be bricked by a single voter by spamming unbounded proposals making the contract become entirely useless. This has effects on other parts of the system as the primary function of governance is to adjust the rollup parameters.

### PoC

Test that shows 1000 proposals can exceed block gas limit.
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

import "forge-std/console.sol";

contract ProposalDOS {

    /// @custom:field voting expiration time
    /// @custom:field executed
    struct ProposalInfo {
        uint256 expirationTime;
    }

    /// @custom:field batchBlockInterval
    /// @custom:field batchMaxBytes
    /// @custom:field batchTimeout
    /// @custom:field maxChunks
    /// @custom:field rollupEpoch
    struct ProposalData {
        uint256 batchBlockInterval;
        uint256 batchMaxBytes;
        uint256 batchTimeout;
        uint256 maxChunks;
        uint256 rollupEpoch;
    }

    /// @notice proposal data
    mapping(uint256 proposalID => ProposalData) public proposalData;

    /// @notice proposal info
    mapping(uint256 proposalID => ProposalInfo) public proposalInfos;

    uint256 length = 1000;

    function setUp() public {
        for (uint256 i; i < length; i++) {
            proposalData[i] = ProposalData(1, 1, 1, 1, 1);
            proposalInfos[i] = ProposalInfo(block.timestamp);
        }
    }

    function test_proposal_dos() public {
        uint256 gasBefore = gasleft();
        console.log("Gas before: ", gasBefore);
        for (uint256 i; i < length; i++) {
            delete proposalData[i];
            delete proposalInfos[i];
        }
        uint256 gasAfter = gasleft();
        console.log("Gas after: ", gasAfter);
        console.log("Gas consumed: ", gasBefore - gasAfter);
    }
}
```
Results:
```solidity
Logs:
  Gas before:  1073720620
  Gas after:  1043362317
  Gas consumed:  30358303
```

### Mitigation

Create a separate function that can be called to clean up the proposals. You may also want to limit the number of active proposals a user might have at one time.