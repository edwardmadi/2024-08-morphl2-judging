Witty Hazel Bear

Medium

# claimWithdrawal and cleanStakerStore can get permanently DOSed

## Summary

[`cleanStakerStore()`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/staking/L1Staking.sol#L297) and [`claimWithdrawal()`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/staking/L1Staking.sol#L307) can get permanently DOSed if many stakers are about to be removed at the same time.

## Vulnerability Detail

In L1Staking.sol [`_cleanStakerStore()`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/staking/L1Staking.sol#L486) can be used by the owner via [`cleanStakerStore()`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/staking/L1Staking.sol#L297) or by a user that wants to get his staked amount via [`claimWithdrawal()`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/staking/L1Staking.sol#L307).

Once a staker is slashed, removed by the owner or withdraws, [`_cleanStakerStore()`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/staking/L1Staking.sol#L486) will have to wait X amount of blocks before being able to remove him from the system.

There is a case where [`cleanStakerStore()`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/staking/L1Staking.sol#L297) and [`claimWithdrawal()`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/staking/L1Staking.sol#L307) gets permanently DOSed since there is no way for the owner or the withdrawer to control how much stakers to be cleaned up - all stakers that are ready to be cleaned will be attempted to be removed.

This can happen if a large number of stakers get slashed, removed by the owner, or decide to collectively withdraw at the same time.

We can test this in Remix. The contract below has all the data structures used in `_cleanStakerStore` (deleteList, deleteableHeight, stakers, stakerSet and stakerIndexes):

```solidity
    contract Stakers2 {
        struct StakerInfo {
            address addr;
            bytes32 tmKey;
            bytes blsKey;
        }

        address[] public deleteList;
        mapping(address stakerAddr => uint256 height) public deleteableHeight;
        mapping(address stakerAddr => StakerInfo) public stakers;
        address[255] public stakerSet;
        mapping(address stakerAddr => uint8 index) public stakerIndexes;
        uint8 counter;

        function addStaker(bytes memory x, uint times) external { // x is 0xa3b1f5c478d6e4b27d9fa0245b8c76f94e21a356dfe9b10c4a859f18d3b2e91fd9e4ab56e789ac24cf57d09812ea64bf4c37e8d23b159e6f78da023d45127b12c59f0b7d623be9cd802f3d15cbae04a17cf256f48b2a9c302ef75a08b4dc3ef59d70a623cb1ed5c92f83ca4e6b7951ed03852f4da0e8b5ce3a09d4b576f10d832f2d659a10e342ec83b57f49d43b259d53a08e4612c5f3f47b256b5a10f20d7a4ef5932d7cce8b41f3a0b7465d20f49cf1a27cb8e73d56f0120c6d2f3ac48a1d59ebf76da41c23ab67e581d3c90b42ef56a1bd239f8e7ac9dbe0fb68f4513b920b7e5d2c37afec923d1bf8046cf13a1c2f73d8d54e79ca123fb8a678cef14928b21d07563f8bca1276ed5982fa
            for (uint i = 0; i < times; i++) {
                address staker = address(uint160(++counter));
                deleteList.push(staker);
                deleteableHeight[staker] = block.number;
                stakerIndexes[staker] = counter;
                stakerSet[stakerIndexes[staker]] = staker;
                stakers[staker] = StakerInfo(staker, 0x89abfedcba9876543210fedcba9876543210abcdefabcdefabcdef1234567890, x);
            }
        }

        function cleanStakerStore() external {
            _cleanStakerStore();
        }

        function _cleanStakerStore() internal {
            uint256 i = 0;

            while (i < deleteList.length) {
                if (deleteableHeight[deleteList[i]] <= block.number) {
                    // clean stakerSet
                    delete stakerSet[stakerIndexes[deleteList[i]] - 1];
                    delete stakerIndexes[deleteList[i]];

                    // clean staker info
                    delete stakers[deleteList[i]];

                    // clean deleteList
                    delete deleteableHeight[deleteList[i]];
                    deleteList[i] = deleteList[deleteList.length - 1];
                    deleteList.pop();
                } else {
                    i++;
                }
            }
        }
    }
```

We can use the addStaker function in the POC to populate the data structures with the necessary data that would be present before cleaning the stakers with `_cleanStakerStore()`. The arguments are random ones with the correct length (talking about the StakerInfo's third argument).

The results:

- removing 254 stakers -> 47_261_833 gas
- removing 170 stakers -> 31_640_841 gas
- removing 150 stakers -> 27_921_557 gas

The current average block gas limit on Ethereum is 30M gas so the transaction will revert.

## Impact

[`cleanStakerStore()`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/staking/L1Staking.sol#L297) and [`claimWithdrawal()`](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/staking/L1Staking.sol#L307) can get permenantly DOSed.

## Code Snippet

https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/staking/L1Staking.sol#L486

## Tool used

Manual Review

## Recommendation

Allow the owner to have the option to delete part of the stakers to get around permanent DOS.