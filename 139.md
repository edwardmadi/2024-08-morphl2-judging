Magic Cobalt Giraffe

High

# Sequencers can get prevented from registering in `L1Staking.sol` as `blsKeys[]` and `tmKeys[]` mappings aren't cleared on staker removal

## Summary
Sequencers can get prevented from registering in `L1Staking.sol` as `blsKeys[]` and `tmKeys[]` mappings aren't cleared on staker removal

NOTE : Whitelisted users cannot be considered  `trusted roles` as there exists mechanisms in the code to penalize them in case they start behaving maliciously.
## Vulnerability Detail
Sequencers can stake their funds and register themselves on L1 using the [`register()`](https://github.com/sherlock-audit/2024-08-morphl2/blob/98e0ec4c5bbd0b28f3d3a9e9159d1184bc45b38d/morph/contracts/contracts/l1/staking/L1Staking.sol#L161) function in `L1Staking.sol` :

```solidity
    function register(bytes32 tmKey, bytes memory blsKey) external payable inWhitelist(_msgSender()) {
        require(stakers[_msgSender()].addr == address(0), "already registered");
        require(tmKey != 0 && !tmKeys[tmKey], "invalid tendermint pubkey");
        require(blsKey.length == 256 && !blsKeys[blsKey], "invalid bls pubkey");
        require(msg.value == stakingValue, "invalid staking value");

        stakers[_msgSender()] = Types.StakerInfo(_msgSender(), tmKey, blsKey);
        _addStaker(_msgSender());
        blsKeys[blsKey] = true;
        tmKeys[tmKey] = true;
        emit Registered(_msgSender(), tmKey, blsKey);

        // send message to add staker on l2
        _msgAddStaker(stakers[_msgSender()]);
    }
```
The inputs required here are the `tmKey` and the `blsKey` which are used in consensus by sequencers to sign blocks.

In case a sequencer starts behaving maliciously and starts committing invalid batches to L1, they can be slashed by the Rollup contract through `slash()` or removed by the `L1Staking` owner through `removeStakers()`

```solidity
    function removeStaker(address[] memory _stakers) external onlyOwner {
        for (uint256 i = 0; i < _stakers.length; i++) {
            require(isActiveStaker(_stakers[i]), "only active staker can be removed");
            require(withdrawals[_stakers[i]] == 0, "withdrawing");

            withdrawals[_stakers[i]] = block.number + withdrawalLockBlocks;
            _removeStaker(_stakers[i]);
            emit Withdrawn(_stakers[i], withdrawals[_stakers[i]]);

            delete whitelist[_stakers[i]];
            removedList[_stakers[i]] = true;
        }
        emit StakersRemoved(_stakers);

        // send message to remove stakers on l2
        _msgRemoveStakers(_stakers);
    }

    /// @notice challenger win, slash sequencers
    function slash(uint256 sequencersBitmap) external onlyRollupContract nonReentrant returns (uint256) {
        address[] memory sequencers = getStakersFromBitmap(sequencersBitmap);

        uint256 valueSum;
        for (uint256 i = 0; i < sequencers.length; i++) {
            if (withdrawals[sequencers[i]] > 0) {
                delete withdrawals[sequencers[i]];
                valueSum += stakingValue;
            } else if (!isStakerInDeleteList(sequencers[i])) {
                // If it is the first time to be slashed
                valueSum += stakingValue;
                _removeStaker(sequencers[i]);
                // remove from whitelist
                delete whitelist[sequencers[i]];
                removedList[sequencers[i]] = true;
            }
        }

        uint256 reward = (valueSum * rewardPercentage) / 100;
        slashRemaining += valueSum - reward;
        _transfer(rollupContract, reward);

        emit Slashed(sequencers);
        emit StakersRemoved(sequencers);

        // send message to remove stakers on l2
        _msgRemoveStakers(sequencers);

        return reward;
    }
```
Both these functions call `_removeStaker()` :

```solidity
  function _removeStaker(address addr) internal {
        require(deleteableHeight[addr] == 0, "already in deleteList");
        deleteList.push(addr);
        deleteableHeight[addr] = block.number + withdrawalLockBlocks;
    }
```

After `withdrawalLockBlocks`, owner can call `cleanStakerStore()`, which calls `_cleanStakerStore()` and clear the staker's data :

```solidity
 function _cleanStakerStore() internal {
        uint256 i = 0;
        while (i < deleteList.length) {
            if (deleteableHeight[deleteList[i]] <= block.number) {
                // clean stakerSet
                delete stakerSet[stakerIndexes[deleteList[i]] - 1];
                delete stakerIndexes[deleteList[i]];

                // clean staker info
                delete stakers[deleteList[i]];

                // clean deleteList
                delete deleteableHeight[deleteList[i]];
                deleteList[i] = deleteList[deleteList.length - 1];
                deleteList.pop();
            } else {
                i++;
            }
        }
    }
```

As you can observe, in the entire flow, the `blsKeys[]` and `tmKeys[]` mappings are never cleared, which can be exploited by a malicious staker to DoS an honest staker in the following way :

- Honest Staker calls `register()`
- A malicious unregistered Staker observes this transaction in the mempool, copies the honest staker's `tmKey` and `blsKey` and front-runs the honest staker's transaction
- The honest staker's call reverts as `blsKeys[blsKey]` and `tmKeys[tmKey]` return true in `register()`
- To rectify this, the owner calls `removeStaker()` and removes the malicious staker successfully

However, since the aforementioned mappings aren't cleared, the honest staker still won't be able to register.
## Impact
Permanent DoS
## Code Snippet

## Tool used

Manual Review

## Recommendation

Clear the mappings on staker removal
