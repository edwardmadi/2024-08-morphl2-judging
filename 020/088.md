Shambolic Banana Barbel

Medium

# `claimAll()` can fail to claim all distributions

### Summary

The loop in `claimAll()` doesn't account for the fact that the list that's being iterated over can change as the loop is executed, which can lead to some delegatees not having their rewards claimed.

### Root Cause

In `Distribute.sol`, the [claimAll()](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l2/staking/Distribute.sol#L200-L218) function is intended to be called by a delegator to claim all their rewards from all delegatees.

```solidity
function claimAll(address delegator, uint256 targetEpochIndex) external onlyL2StakingContract {
    require(mintedEpochCount != 0, "not minted yet");
    uint256 endEpochIndex = (targetEpochIndex == 0 || targetEpochIndex > mintedEpochCount - 1)
        ? mintedEpochCount - 1
        : targetEpochIndex;
    uint256 reward;
    for (uint256 i = 0; i < unclaimed[delegator].delegatees.length(); i++) {
        address delegatee = unclaimed[delegator].delegatees.at(i);
        if (
            unclaimed[delegator].delegatees.contains(delegatee) &&
            unclaimed[delegator].unclaimedStart[delegatee] <= endEpochIndex
        ) {
            reward += _claim(delegatee, delegator, endEpochIndex);
        }
    }
    if (reward > 0) {
        _transfer(delegator, reward);
    }
}
```
As we can see, we take the full list of delegatees in the `unclaimed` mapping and iterate over them, claiming for each.

However, as `_claim()` is called, the `delegatees` list will change, because delegatees where their `unclaimedEnd` has been hit will be removed:
```solidity
if (unclaimed[delegator].undelegated[delegatee] && unclaimed[delegator].unclaimedEnd[delegatee] == i) {
    unclaimed[delegator].delegatees.remove(delegatee);
    delete unclaimed[delegator].undelegated[delegatee];
    delete unclaimed[delegator].unclaimedStart[delegatee];
    delete unclaimed[delegator].unclaimedEnd[delegatee];
    break;
}
```
The result is that the length of the array can change, shifting the `delegatees` array to the left and skipping the next delegatee.

### Internal Preconditions

1. Multiple delegatees must be on the list, with the first reaching an `unclaimedEnd` before the later.

### External Preconditions

None

### Attack Path

1. User calls `claimAll()` at a time when an earlier delegatee has reached their `unclaimedEnd`, but later delegatees have not.
2. The `claimAll()` function will claim everything from the finished delegatees, but will skip claiming from the following one.

### Impact

The `claimAll()` function will not claim all of a user's funds.

### PoC

The following test can be added to `Distribute.t.sol` to demonstrate the attack:
```solidity
function testPapa_claimAllDoesntClaimAll() public {
    // Transfer 100 ether to the distribute contract from multisig to set up
    hevm.prank(multisig);
    morphToken.transfer(address(distribute), 100 ether);

    // Notify delegation from two stakers: 10 ether and 5 ether to Alice
    hevm.startPrank(address(l2Staking));
    distribute.notifyDelegation(firstStaker, alice, 0, 10 ether, 10 ether, 1, true);
    distribute.notifyDelegation(secondStaker, alice, 0, 5 ether, 5 ether, 2, true);
    distribute.notifyUndelegation(firstStaker, alice, 1, 10 ether, 1);
    hevm.stopPrank();

    // Update the epoch reward for epoch 0.
    _update_epoch_reward(0);
    _update_epoch_reward(1);

    // Confirm there is no balance before
    assertEq(morphToken.balanceOf(alice), 0);

    // First staker undelegated after 1 epoch, so should owe mockReward
    uint owedFromStaker1 = distribute.queryUnclaimed(firstStaker, alice);
    assertEq(owedFromStaker1, mockReward);

    // Second staker is owed from epochs 0 and 1, so should be mockReward * 2
    uint owedFromStaker2 = distribute.queryUnclaimed(secondStaker, alice);
    assertEq(owedFromStaker2, mockReward * 2);

    // Claim all rewards for Alice for epoch 0 and 1
    hevm.prank(address(l2Staking));
    distribute.claimAll(alice, 1);
    uint256 rewardAfter = morphToken.balanceOf(alice);

    // After claiming, we should have mockReward * 3, but we skipped staker 2
    assertEq(rewardAfter, mockReward);
}
```

### Mitigation

The value for `i` in the loop should only be incremented in the event that the `delegatee` was not removed from the list.
