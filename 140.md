Magic Cobalt Giraffe

Medium

# `claimReward()` will only partially claim rewards in specific cases, due to incorrect traversal logic

## Summary
[`claimReward()`](https://github.com/sherlock-audit/2024-08-morphl2/blob/98e0ec4c5bbd0b28f3d3a9e9159d1184bc45b38d/morph/contracts/contracts/l2/staking/L2Staking.sol#L439) will only partially claim rewards in specific cases, due to incorrect traversal logic
## Vulnerability Detail
Rewards can be claimed through the `claimReward()` function of `L2Staking.sol` :
```solidity
    /// @notice delegator claim reward
    /// @param delegatee         delegatee address, claim all if empty
    /// @param targetEpochIndex  up to the epoch index that the delegator wants to claim
    function claimReward(address delegatee, uint256 targetEpochIndex) external nonReentrant {
        if (delegatee == address(0)) {
            IDistribute(DISTRIBUTE_CONTRACT).claimAll(_msgSender(), targetEpochIndex);
        } else {
            IDistribute(DISTRIBUTE_CONTRACT).claim(delegatee, _msgSender(), targetEpochIndex);
        }
    }
```
Notice that this calls `claimAll()`, if the user wishes to claim all their rewards at once.

```solidity

    /// @dev claim delegation reward of all sequencers.
    /// @param delegator         delegator address
    /// @param targetEpochIndex  the epoch index that the user wants to claim up to
    ///
    ///  If targetEpochIndex is zero, claim up to latest mint epoch,
    ///  otherwise it must be greater than the last claimed epoch index.
    function claimAll(address delegator, uint256 targetEpochIndex) external onlyL2StakingContract {
        require(mintedEpochCount != 0, "not minted yet");
        uint256 endEpochIndex = (targetEpochIndex == 0 || targetEpochIndex > mintedEpochCount - 1)
            ? mintedEpochCount - 1
            : targetEpochIndex;
        uint256 reward;
        for (uint256 i = 0; i < unclaimed[delegator].delegatees.length(); i++) {
            address delegatee = unclaimed[delegator].delegatees.at(i);
            if (
                unclaimed[delegator].delegatees.contains(delegatee) &&
                unclaimed[delegator].unclaimedStart[delegatee] <= endEpochIndex
            ) {
                reward += _claim(delegatee, delegator, endEpochIndex);
            }
        }
        if (reward > 0) {
            _transfer(delegator, reward);
        }
    }
```
This function loops through the `unclaimed[]` enumerable set for each delegatee that the delegators receives rewards from.

Let's look at the `_claim()` function :

```solidity
function _claim(address delegatee, address delegator, uint256 endEpochIndex) internal returns (uint256 reward) {
        require(unclaimed[delegator].delegatees.contains(delegatee), "no remaining reward");
        require(unclaimed[delegator].unclaimedStart[delegatee] <= endEpochIndex, "all reward claimed");

        for (uint256 i = unclaimed[delegator].unclaimedStart[delegatee]; i <= endEpochIndex; i++) {
            // compute delegator epoch reward
            reward +=
                (distributions[delegatee][i].delegatorRewardAmount * distributions[delegatee][i].amounts[delegator]) /
                distributions[delegatee][i].delegationAmount;

            // if claimed end epoch is reached, next distribution has been updated when undelegate
            if (!unclaimed[delegator].undelegated[delegatee] || unclaimed[delegator].unclaimedEnd[delegatee] != i) {
                // if delegator has not finished the claim and distribution not contains the delegator's info in next epoch,
                // migrate delegator info to next epoch.
                if (!distributions[delegatee][i + 1].delegators.contains(delegator)) {
                    distributions[delegatee][i + 1].delegators.add(delegator);
                    distributions[delegatee][i + 1].amounts[delegator] = distributions[delegatee][i].amounts[delegator];
                }

                // if next distribution is empty, migrate distribution to next epoch
                if (distributions[delegatee][i + 1].delegationAmount == 0) {
                    distributions[delegatee][i + 1].delegationAmount = distributions[delegatee][i].delegationAmount;
                    distributions[delegatee][i + 1].remainsNumber = distributions[delegatee][i].remainsNumber;
                }
            }

            // update distribution info, delete if all claimed
            distributions[delegatee][i].remainsNumber--;
            if (
                distributions[delegatee][i].remainsNumber == 0 && // all delegators claimed
                distributions[delegatee][i].commissionAmount == 0 // delegatee claimed
            ) {
                delete distributions[delegatee][i];
            }

            // if undelegated, remove delegator unclaimed info after claimed all
            if (unclaimed[delegator].undelegated[delegatee] && unclaimed[delegator].unclaimedEnd[delegatee] == i) {
                unclaimed[delegator].delegatees.remove(delegatee);
                delete unclaimed[delegator].undelegated[delegatee];
                delete unclaimed[delegator].unclaimedStart[delegatee];
                delete unclaimed[delegator].unclaimedEnd[delegatee];
                break;
            }
        }
        unclaimed[delegator].unclaimedStart[delegatee] = endEpochIndex + 1;
        emit RewardClaimed(delegator, delegatee, endEpochIndex, reward);
    }
```

Notice these lines :

```solidity
            // if undelegated, remove delegator unclaimed info after claimed all
            if (unclaimed[delegator].undelegated[delegatee] && unclaimed[delegator].unclaimedEnd[delegatee] == i) {
                unclaimed[delegator].delegatees.remove(delegatee);
                delete unclaimed[delegator].undelegated[delegatee];
                delete unclaimed[delegator].unclaimedStart[delegatee];
                delete unclaimed[delegator].unclaimedEnd[delegatee];
                break;
            }
```
If this is executed, the delegatee is removed from the `unclaimed[]` set and therefore the size of the set decreases by 1.

This is an issue, as the delegatee at index `i+1` will be pushed back to index `i`, but `i` will still be incremented to `i+1` to continue the loop. This means that the delegatee at `i+1` will be skipped and rewards won't be claimed for that particular delegatee
## Impact
Broken functionality 

## Code Snippet

## Tool used

Manual Review

## Recommendation
Alter such that if the `if` condiiton in `_claim` is met, `i` isn't incremented