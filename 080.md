Shambolic Banana Barbel

Medium

# Verifier can be used for batches it was not intended for

### Summary

Due to a missing check in `getVerifier()`, if a proof is requested for a batch index prior to the `startBatchIndex` of the first verifier, ZK proofs will still be checked, which could allow fradulent proofs to be accepted.

### Root Cause

In `MultipleVersionRollupVerifier.sol`, the `getVerifier()` function is used to determine which verifier contract is appropriate for a given batch. This is critically important, as using the wrong verifier can create a mismatch in passed values and allow invalid proofs to be accepted.

[The function is implemented as follows](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l1/rollup/MultipleVersionRollupVerifier.sol#L95-L112):
```solidity
function getVerifier(uint256 _version, uint256 _batchIndex) public view returns (address) {
    // Normally, we will use the latest verifier.
    Verifier memory _verifier = latestVerifier[_version];

    if (_verifier.startBatchIndex > _batchIndex) {
        uint256 _length = legacyVerifiers[_version].length;
        // In most case, only last few verifier will be used by `Rollup`.
        // So, we use linear search instead of binary search.
        unchecked {
            for (uint256 i = _length; i > 0; --i) {
                _verifier = legacyVerifiers[_version][i - 1];
                if (_verifier.startBatchIndex <= _batchIndex) break;
            }
        }
    }

    return _verifier.verifier;
}
```
Each legacy verifier is saved with both a `startBatchIndex` (the first batch that it is appropriate to use it for) and a `verifier` (the address of the verifier contract).

The function iterates backwards through legacy verifiers until it finds one where the `startBatchIndex` is prior to the batch index being verified. It then breaks and returns this verifier's address.

However, in the situation where this condition is never met (in other words, where we are trying to verify a batch prior to the earliest verifier), the function succeeds and returns the first verifier.

### Internal Preconditions

1. The first verifier in the legacyVerifiers list must have a `startBatchIndex` greater than 0.
2. The batch being requested must be less than the `startBatchIndex` of the first verifier.

### External Preconditions

None

### Attack Path

1. The first verifier has a `startBatchIndex` greater than 0.
2. An attacker creates a fraudulent proof for a previous batch (which was not intended to work with this verifier) to be accepted.
3. The attacker submits this proof with a batch index less than the `startBatchIndex` of the first verifier.

### Impact

The `MultipleVersionRollupVerifier` contract will send proofs to an incorrect verifier if they are submitted with a batch index less than the `startBatchIndex` of the first verifier, which can lead to

### PoC

The follwing test can be added to `MultipleVersionRollupVerifier.t.sol` to demonstrate the attack:
```solidity
function testPapa_getUnstartedVerifier() external {
    verifier.initialize(address(rollup));
    uint version = 50;

    verifier.updateVerifier(version, 100, address(v0));
    verifier.updateVerifier(version, 200, address(v1));
    verifier.updateVerifier(version, 300, address(v2));

    assertEq(verifier.getVerifier(version, 1), address(v0));
}
```

### Mitigation

In the event that we never break from the loop in `getVerifier()`, the function should revert.
