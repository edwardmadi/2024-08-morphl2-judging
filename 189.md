Shallow Clay Bear

Medium

# Incorrect call to `isSequencer` instead of `isCurrentSequencer` in `onlySequencer` modifier allows stakers not yet included in the sequencer set to participate in governance

### Summary

In `Gov.sol`, calling `isSequencer()` instead of `isCurrentSequencer` to check if the caller is a sequencer is incorrect, as `isSequencer()` does not take into account the 2 block waiting period required to actually consider that a staker actually belongs to the sequencer set.

### Root Cause

Found in [Gov.sol#L70](https://github.com/sherlock-audit/2024-08-morphl2/blob/main/morph/contracts/contracts/l2/staking/Gov.sol#L70).

The `createProposal()` and `voteProposal()` calls in `Gov.sol` can only be called by addresses that are currently included in the sequencer set (i.e sequencers). In order to ensure this, such calls are protected with the `onlySequencer` modifier, which will call the `isSequencer()` call from the `Sequencer.sol` contract to verify wether the caller is actually in the sequencer set. As shown in the following snippet, `isSequencer()` directly returns wether the value is stored in `sequencerSet2`:

```solidity
// Sequencer.sol
function isSequencer(address addr) external view returns (bool) {
		return _contains(sequencerSet2, addr); 
}
```

However, when updating the sequencer set via `updateSequencerSet()` in `Sequencer.sol`, there is a 2-block delay restriction to consider if the current sequencer set is actually final or not:

```solidity
// Sequencer.sol
function updateSequencerSet(address[] calldata newSequencerSet) public onlyL2StakingContract {
        // sequencerSet changes will take effect after two blocks
        // The current block height +2 can only be greater than or equal to the last record
        if ((block.number + 2) > blockHeight2) {
            blockHeight0 = blockHeight1;
            blockHeight1 = blockHeight2;
            blockHeight2 = block.number + 2; 

            sequencerSet0 = sequencerSet1;
            sequencerSet1 = sequencerSet2;
            sequencerSet2 = newSequencerSet;
        } else {
            sequencerSet2 = newSequencerSet;
        }

        ...
    }
```

This means that changes in the sequencer set will only take effect after two blocks. As it can be seen in `updateSequencerSet`, if two blocks have not passed, `sequencerSet2` will still be updated, which contains the newest sequencer set. However, the current sequencer set is not given by data stored in `sequencerSet2`, and should instead be given by considering the amount of blocks passed since the last update to the set. This is correctly reflected in `Sequencer.sol`'s `isCurrentSequencer()`, which compares the current `block.number` with the stored block heights to determine which sequencer set variable should be returned:

```solidity
// Sequencer.sol
function isCurrentSequencer(address addr) external view returns (bool) {
        if (block.number >= blockHeight2) {
            return _contains(sequencerSet2, addr);
        }
        if (block.number >= blockHeight1) {
            return _contains(sequencerSet1, addr);
        }
        return _contains(sequencerSet0, addr);
    }
```

Because `sequencerSet2` will always be updated regardless of block restrictions and `isSequencer()` directly checks if the address is contained in `sequencerSet2`, the call to `isSequencer()` in `Gov.sol` won’t be querying the most updated sequencer set.

This can lead to situations where addresses that should not be considered to belong in the sequencer set are considered as such, allowing temporary sequencers to create proposals, and even vote and execute them in some scenarios.

### Internal pre-conditions

1. The sequencer set is updated several times.
2. A user leverages being temporarily in the sequencer set to interact with the governance contract.

### External pre-conditions

None.

### Attack Path

The situation described in “Root cause” can lead to the following attack path:

1. There is a proposal with one remaining vote to be passed. However, none of the remaining voters in the sequencer set wants to vote for it.
    
    Note: the proposal only having one remaining vote to be passed is a requirement, as the `_checkPassed()` internal function triggered when voting will only consider votes as valid if the caller belongs to `sequencerSet2`:
    
    ```solidity
    function _checkPassed(uint256 proposalID) internal view returns (bool) {
            // checking invalidate votes
            address[] memory latestSequencerSet = ISequencer(SEQUENCER_CONTRACT).getSequencerSet2();
            uint256 validVotes = 0;
            for (uint256 i = 0; i < latestSequencerSet.length; i++) {
                if (votes[proposalID].contains(latestSequencerSet[i])) { 
                    validVotes = validVotes + 1;
                }
            }
            return validVotes > (latestSequencerSet.length * 2) / 3;  
        } 
    ```
    
2. A staker holding a relatively big amount of MORPH so that he can out-delegate some sequencers currently included in the set delegates MORPH to himself. The sequencer set is updated, and a call to `Sequencer.updateSequencerSet()` is triggered, updating `sequencerSet2`, `sequencerSet1` and `sequencerSet0`. 
3. The staker now interacts with `Gov.sol`. Because `Gov.sol` does not consider the two block delay (given that it calls `isSequencer()` instead of `isCurrentSequencer()` to check if caller is a sequencer), the staker is able to bypass sequencer and the 2-block delay restrictions.
4. The staker can then vote for the proposal. Because it is the last vote to make the proposal executable, the proposal gets executed.
5. Finally, the staker undelegates MORPH tokens. The sequencer set is updated and the staker is removed from the sequencer set. 

Note that there is another possible scenario, where several delegators are delegating to a wide range of stakers at the same time, making the sequencer set be updated frequently, and making sequencers to be able to vote or create proposals when they shouldn’t be able to.

### Impact

Any staker can bypass sequencer restrictions in the governance contract, allowing stakers that should still not be considered sequencers to create proposals. In some situations, stakers can even vote and execute proposals, which alters the current design of the governance system, potentially leading to network parameters being configured to the ones set by a proposal that shouldn’t have passed.

### PoC

_No response_

### Mitigation

In the `onlySequencer` modifier in`Gov.sol`, check wether the caller is an actual sequencer by calling the `isCurrentSequencer()` function, instead of `isSequencer()`:

```diff
// Gov.sol

modifier onlySequencer() {
-        bool _in = ISequencer(SEQUENCER_CONTRACT).isSequencer(_msgSender()); 
+        bool _in = ISequencer(SEQUENCER_CONTRACT).isCurrentSequencer(_msgSender()); 
        require(_in, "only sequencer allowed");
        _;
    }
```